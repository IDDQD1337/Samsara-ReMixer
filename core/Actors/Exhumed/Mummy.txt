Actor ExMummy
{
    obituary "%o received an ancient curse from a mummy."
    hitobituary "%o got whacked in the head by a mummy's staff."
    scale 0.60
    radius 16
    height 52
    health 400
	Tag "Ancient Mummy"
    speed 6
    meleerange 45
	painchance 70
    mass 900
    seesound "ancientmummy/active1"
    activesound "ancientmummy/active"
	painsound "ancientmummy/pain"
    deathsound "ancientmummy/death"
	DropItem "PowerslaveSoulPowerOrbSpawn" 64,1	
	PainChance "ShrinkerDMG", 0
    MONSTER
	+NOBLOOD
    +BOSSDEATH
    +FLOORCLIP
	+DONTRIP	
    +MISSILEMORE
	+NOINFIGHTING
	Species "Enemies"	
    States
    {
    Spawn:
		PSMU F 0
		PSMU F 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		PSMU F 1 ACS_ExecuteAlways(668,0,1,0)
		PSMU F 0 A_GiveInventory("AlreadySpawned",1)
		PSMU F 0 A_JumpIfInventory("InvisibleToken",1,2)
		PSMU F 0 A_Jump(256,2)
		PSMU F 0 A_ChangeFlag("STEALTH",1)
	Spawn2:
		PSMU F 5 A_Look
		loop
	Idle:
		PSMU F 5 A_Look
		Loop
    See:
		PSMU ABCDEF 4 A_Chase
		loop
    Missile:
		TNT1 A 0 A_Jump(128,"Missile2")
		PSMU A 0 A_PlaySound("ancientmummy/attackchant")
		PSMU G 10 A_FaceTarget
		PSMU HIJ 5 A_FaceTarget
		PSMU K 0 A_PlayWeaponSound("ancientmummy/hitstaffonground")
		PSMU K 5 bright A_FaceTarget
		PSMU L 5 bright A_CustomMissile("MummySpiritProjectile",32,0,0)
		PSMU M 5 bright A_FaceTarget
		PSMU N 15
		goto See
	Missile2:
		PSMU A 0 A_PlaySound("ancientmummy/attackchant")
		PSMU G 10 A_FaceTarget
		PSMU HIJ 5 A_FaceTarget
		PSMU K 0 A_PlayWeaponSound("ancientmummy/hitstaffonground")
		PSMU K 5 bright A_FaceTarget
		PSMU L 5 bright A_CustomMissile("MummySpiritProjectile2",32,0,0)
		PSMU M 5 bright A_FaceTarget
		PSMU N 15
		goto See
    Melee:
		TNT1 A 0 A_PlayWeaponSound("ancientmummy/swingstaff")
		PSMU O 7 A_FaceTarget
		PSMU P 7 A_CustomMeleeAttack(random(3, 5) * 6, "ancientmummy/staffhitu")
		TNT1 A 0 A_PlayWeaponSound("ancientmummy/swingstaff")
		PSMU Q 7 A_FaceTarget
		PSMU P 6 A_CustomMeleeAttack(random(3, 5) * 6, "ancientmummy/staffhitu")
		PSMU O 7
		goto See
	Pain:
		PSMU F 2
		PSMU F 3 A_Pain
		goto See
    Death:
	XDeath:
	    TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)	
		PSMU R 6 A_Scream
		PSMU RRRSTUV 5
		PSMU W 4 A_PlayWeaponSound("ancientmummy/unwrap")
		TNT1 A 0 A_SpawnItem("UniversalBloodyHellGoreXDeathEffect")
		PSMU XY 4
		TNT1 A 0 A_NoBlocking
		PSMU Z -1 A_KillMaster
		stop
    Raise:
		PSMU Z 0 A_PlaySound("ancientmummy/raise")
		PSMU ZYX 2 A_SetInvulnerable
		PSMU VUTSRRR 3
		PSMU A 0 A_PlaySound("ancientmummy/evillaugh")
		PSMU A 0 A_UnSetInvulnerable
		goto See
	Death.DukeIce:
		"####" "#" 0 A_PlaySound("duke/enemyfreeze")
		"####" "#" 0 A_GiveInventory("FreezethrowerWeakness")
		"####" "#" 0 A_Jump(218,"Ice")
		"####" "#" 0 A_GiveToTarget("DukeKillTaunt",1)
	Ice:
	Death.HexenIce:
	Death.WTMeleeIce:
	Death.WTRangedIce:
	Death.Ice:
		"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		"####" "#" 0 A_NoBlocking
		"####" "#" 0 A_KillMaster
		"####" "#" 0 A_GenericFreezeDeath
		"####" "#" 70 A_UnsetSolid
		"####" "#" 0 A_IceGuyDie
		"####" "#" 1 A_IceGuyDie
		TNT1 A 5
		stop					
	Death.HLLaser:
    Death.HLExplosive:
    Death.HLRPGExplosive:	
		TNT1 A 0 A_JumpIfHealthLower(-20,1)
		goto Death
    Death.HLEgon:
    Death.FreemanGibs:
		TNT1 A 0 A_SpawnItem("HLGenericGibSpawner")
		Goto XDeath
	Death.LifeLeech:
	Death.CalebNapalm:
	Death.Vodoo:
	Death.VoodooPuff:
	Death.DynamiteCoop:
	Death.Dynamite:	  
	Death.Shock:	
		TNT1 A 0 A_JumpIfHealthLower(-50, "XDeathForCaleb")
	Death.Tesla:
	Death.CalebFire:
	Death.CalebBullet:
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
		Goto Death
	XDeathForCaleb:
	    TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_SpawnItemEx("CalebGibSpawner")
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("CalebDMTaunt1",1)
		Goto XDeath		
	Death.EradExplosive:
	Death.EradNova:
	Death.EradShock:
	Death.EradExplosive2:	
		TNT1 A 0 A_JumpIfHealthLower(-5,"XDeathForCatBitch")
    Death.EradDisc:
	Death.EradPiercingDart:
	Death.EradGreenFire:
	Death.EradCoopGreenFire:
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("EleenaDMTaunt1", 1)
		Goto Death
	XDeathForCatBitch:
	    TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("EleenaGibTaunt",1)
		Goto XDeath
	Death.DukeExplosive:
	Death.DukeDamage:
	Death.DukePistol:
		TNT1 A 0 A_Jump(218,"Death")
		TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
		goto Death
	Death.MightyBoot:
		TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
		TNT1 A 0 A_Jump(64,"Death")
		TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
		goto Death
	Death.PSRAPower:
	Death.PSRAPowerCoop:
	Death.PSExplosive:
	Death.PSExplosiveCoop:
		TNT1 A 0 A_JumpIfHealthLower(-30, "XDeathForJon")
	Death.PSBullet:
	Death.PSFire:	
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("JonDMTaunt1", 1)
		Goto Death
	XDeathForJon:
	    TNT1 A 0 A_UnsetSolid
        TNT1 A 0 A_Jump(96,"XDeath")
        TNT1 A 0 A_GiveToTarget("JonDMTaunt1",1)
		Goto XDeath		
	Death.LeonardDamage2:
	Death.LeonardDamage3:
		TNT1 A 0 A_JumpIfHealthLower(-20, "XDeathForLeonard")
	Death.LeonardDamage0:
	Death.LeonardDamage1:	
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("LeonardDMTaunt1", 1)
		Goto Death
	XDeathForLeonard:
		TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_SpawnItemEx("RRGibSpawner")
		TNT1 A 0 A_Jump(96,"XDeath")
		TNT1 A 0 A_GiveToTarget("LeonardGibTaunt",1)
		Goto XDeath		
	Death.ZorchPistol:
	Death.Zorch:
	Death.ZorchKamikaze:
	Death.PhasingZorch:
	Death.ZorchSpectral:
		"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		"####" "#" 0 A_Stop
		"####" "#" 0 A_ChangeFlag("DONTGIB",1)
		"####" "#" 0 A_PlaySoundEx("chex/zorch","soundslot5",0,0)
		"####" "#" 3 A_SpawnItem("ZorchEffectSmall")
		"####" "#" 0 A_GiveInventory("EnemyZorched",1)
		"####" "#" 3 A_SetTranslucent(0.9,0)
		"####" "#" 0 A_Scream
		"####" "#" 3 A_SetTranslucent(0.8,0)
		"####" "#" 0 A_NoBlocking
		"####" "#" 3 A_SetTranslucent(0.7,0)
		"####" "#" 3 A_SetTranslucent(0.6,0)
		"####" "#" 3 A_SetTranslucent(0.5,0)
		"####" "#" 3 A_SetTranslucent(0.4,0)
		"####" "#" 3 A_SetTranslucent(0.3,0)
		"####" "#" 3 A_SetTranslucent(0.2,0)
		"####" "#" 3 A_SetTranslucent(0.1,0)
		TNT1 A 1 A_SetTranslucent(1,0)
		TNT1 A -1 A_KillMaster
		stop
	Death.SWFist:
	Death.SWKatana:
	Death.SWShuriken:
	Death.SWSticky:
	Death.SWStickyCoop:	
	Death.SWGrenade:
	Death.SWNuke:
	Death.SWRocket:
	Death.SWRailgun:
	Death.SWRailgunCoop:
	Death.SWNukeCoop:
	Death.SWFlame:
	Death.SWFlame2:
	Death.SWFlame2Coop:
	Death.SWFlame3:	
		TNT1 A 0 A_JumpIfHealthLower(-3,"XDeathForWang")
    Death.SWBullet:	  
		TNT1 A 0 A_Jump(218, "Death")
		TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
		goto Death
	XDeathForWang:
	    TNT1 A 0 A_UnsetSolid
		TNT1 A 0 A_SpawnItemEx("SWHumanGibSpawner")	
		TNT1 A 0 A_Jump(218, "XDeath")	
        TNT1 A 0 A_GiveToTarget("LoWangDMTaunt2",1)
		goto XDeath	
	Death.RottFire:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItem("BurningSkeleton")
		TNT1 A 0 A_NoBlocking		
		TNT1 A 5 A_KillMaster
		Stop	
	Death.RottExplosive:
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_SpawnItemEx("RottGibbedMan",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
		TNT1 A 1
		TNT1 A 0 A_Jump(128,2)
		TNT1 A 0 A_GiveToTarget("LudicrousGibsItem", 1)
		TNT1 AA 0
		TNT1 A 0 A_NoBlocking
		TNT1 A 5 A_KillMaster
		Stop
	Death.RottDisintegrate:
		TNT1 A 0 A_ChangeFlag("SOLID", 0)
		TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		TNT1 A 0 A_Scream
		TNT1 A 0 A_SpawnItem("RGodModeDeath")
		TNT1 A 5 A_KillMaster
		stop	
	Disintegrate:
		"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		"####" "#" 0 A_NoBlocking
		"####" "#" 0 A_ChangeFlag("DONTGIB",1)
		"####" "#" 0 ACS_ExecuteAlways(2530,0,1)
		"####" "#" 0 A_Scream
		"####" "#" 0 A_ChangeFlag("SOLID",FALSE)
		"####" "#" 0 A_ChangeFlag("SHOOTABLE",FALSE)
		"####" "#" 0 A_ChangeFlag("NOGRAVITY",TRUE)
		"####" "#" 0 A_ChangeFlag("FLOORCLIP",TRUE)
		"####" "#" 0 A_Stop
		"####" "#" 3 A_SpawnItem("DisintEffectSmall",0,10)
		"####" "#" 3 A_SetTranslucent(0.9,0)
		"####" "#" 0 ACS_ExecuteAlways(2530,0,1)
		"####" "#" 3 A_SetTranslucent(0.8,0)
		"####" "#" 0 ACS_ExecuteAlways(2530,0,1)
		"####" "#" 3 A_SetTranslucent(0.7,0)
		"####" "#" 0 ACS_ExecuteAlways(2530,0,1)
		"####" "#" 3 A_SetTranslucent(0.6,0)
		"####" "#" 0 ACS_ExecuteAlways(2530,0,1)
		"####" "#" 3 A_SetTranslucent(0.5,0)
		"####" "#" 0 ACS_ExecuteAlways(2530,0,1)
		"####" "#" 2 A_SetTranslucent(0.4,0)
		"####" "#" 0 ACS_ExecuteAlways(2530,0,1)
		"####" "#" 2 A_SetTranslucent(0.3,0)
		"####" "#" 0 ACS_ExecuteAlways(2530,0,1)
		"####" "#" 2 A_SetTranslucent(0.2,0)
		"####" "#" 0 ACS_ExecuteAlways(2530,0,1)
		"####" "#" 2 A_SetTranslucent(0.1,0)
        TNT1 A 5 A_KillMaster
        stop
	Death.Nuke:
		"####" "#" 0 A_ChangeFlag("SOLID", 0)
		"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
		"####" "#" 0 A_Scream
		"####" "#" 0 A_NoBlocking		
		"####" "#" 0 A_SpawnItem("WTNukeDeathSmall")
		"####" "#################" 3 A_Fadeout(0.05)
		TNT1 A 5 A_KillMaster
		stop	
  }
}

Actor MummySpiritProjectile
{
    Speed 16
    damage 6
    scale 0.6
    radius 6
    height 12
    //RENDERSTYLE ADD
	RenderStyle SoulTrans
    Alpha 0.85
    PROJECTILE
    +SEEKERMISSILE
    seesound "ancientmummy/skullmoveinair"
    deathsound "ancientmummy/balhit"
    states
    {
    Spawn:
		PSMM ABC 5 Bright A_SeekerMissile(25,10)
		PSMM A 0 Bright A_PlaySound("ancientmummy/skullmoveinair")
		PSMM DEF 2 Bright A_SeekerMissile(25,10)
		goto Spawn+3
    Death:
		PSMM GGHHIIJJKKLLMM 2 Bright A_FadeOut(0.05)
		stop
    }
}

Actor MummySpiritProjectile2
{
    Speed 35
    damage 6
    scale 0.75
    radius 6
    height 12
	RenderStyle SoulTrans
    Alpha 0.95
    PROJECTILE
    +SEEKERMISSILE
    seesound "ancientmummy/skullmissile5"
    deathsound "ancientmummy/skullmissile"
    states
    {
    Spawn:
		PSM9 ABC 2 Bright
		PSM9 DEF 2 Bright
		goto Spawn+2
    Death:
		PSM9 GGHHIIJJKKLLMM 2 Bright A_FadeOut(0.05)
		stop
    }
}

/*
Actor MummyResurrector
{
    Speed 34
    radius 60
    height 60
    +NOCLIP
    -SHOOTABLE
    -SOLID
    states
    {
    Spawn:
		TNT1 A 1
		TNT1 AAAAAAAAAAAA 1 A_VileChase
		stop
    Heal:
		TNT1 A 1 A_JumpIfInventory("ImAFuckingMummy",1,1)
		Goto Death
		TNT1 A 1
		stop
    Death:
		TNT1 A 1 A_FadeOut
		Loop
    }
}*/