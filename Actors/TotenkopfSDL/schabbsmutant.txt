ACTOR TotenkopfSchabbsMutant : TotenkopfEnemy 
{
	Health 1550
	Radius 32
	Height 64
	Speed 6
	Mass 1000
	PainChance 0
	+FLOORCLIP
    +NOTDMATCH
	+DONTRIP	
    +THRUGHOST
	+MISSILEMORE
	+LOOKALLAROUND
	+BOSS
	Tag "Dr Schabbs (Mutant)"
	Seesound "Totenkopf/Boss2Sight"
	Deathsound "Totenkopf/Boss2Death"
	Activesound ""
	Obituary "%o was slaughtered by a mutant Doctor Schabbs."
	States 
	{
		Spawn:
			TK41 A 1 A_Look
			Loop
		See:
			"####" AAABBCCCDD 2 A_Chase
			Loop
		Missile:
			"####" E 15 A_FaceTarget
			"####" F 0 A_Playsound("Totenkopf/atkchaingun", CHAN_WEAPON)
			"####" F 0 A_CustomMissile("Totenkopf_Tracer_Mutagen",40,0,random(-1,1))
			"####" F 6 Bright A_FaceTarget
			"####" G 0 A_Playsound("Totenkopf/atkchaingun", CHAN_WEAPON)
			"####" G 0 A_CustomMissile("Totenkopf_Tracer_Mutagen",40,0,random(-1,1))
			"####" G 0 A_CustomMissile("TotenkopfNeedle",40,-32)
			"####" G 0 A_CustomMissile("TotenkopfNeedle",40,32)
			"####" G 6 Bright A_FaceTarget
			"####" F 0 A_Playsound("Totenkopf/atkchaingun", CHAN_WEAPON)
			"####" F 0 A_CustomMissile("Totenkopf_Tracer_Mutagen",40,0,random(-1,1))
			"####" F 6 Bright A_FaceTarget
			"####" G 0 A_Playsound("Totenkopf/atkchaingun", CHAN_WEAPON)
			"####" G 0 A_CustomMissile("Totenkopf_Tracer_Mutagen",40,0,random(-1,1))
			"####" G 0 A_CustomMissile("TotenkopfNeedle",40,-32)
			"####" G 0 A_CustomMissile("TotenkopfNeedle",40,32)
			"####" G 6 Bright A_FaceTarget
			"####" F 0 A_Playsound("Totenkopf/atkchaingun", CHAN_WEAPON)
			"####" F 0 A_CustomMissile("Totenkopf_Tracer_Mutagen",40,0,random(-1,1))
			"####" F 6 Bright A_FaceTarget
			"####" G 0 A_Playsound("Totenkopf/atkchaingun", CHAN_WEAPON)
			"####" G 0 A_CustomMissile("Totenkopf_Tracer_Mutagen",40,0,random(-1,1))
			"####" G 0 A_CustomMissile("TotenkopfNeedle",40,-32)
			"####" G 0 A_CustomMissile("TotenkopfNeedle",40,32)
			"####" G 6 Bright A_FaceTarget
			Goto See
		Death:
		XDeath:					
			"####" H 60 A_Scream			
			"####" I 7
			"####" J 8 A_NoBlocking
			"####" K -1 A_KillMaster
			Stop
	}
}

Actor Totenkopf_Tracer_Mutagen : Totenkopf_Tracer_Enemy 
{ 
	Damage (3+(random(0,256)%10))
	DamageType "TotenkopfMutagen"
	States
	{
		Death:
		Crash:
			TNT1 A 0 A_Playsound("Totenkopf/Ricochet", CHAN_BODY)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_Tracer",2,24,40) == 1, 1)
			TK57 BCDE 2 Bright
			Stop
		XDeath:
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_Tracer",2,24,40) == 1, 1)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 1 Bright
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1
			Stop
	}
}

ACTOR TotenkopfNeedle
{
	Radius 8
	Height 6
	Speed 15
	Damage (random(0,32)>>3)
	Projectile
	SeeSound "Totenkopf/SchabbsThrow"
	DeathSound ""
	Obituary "%o was injected with Doctor Schabb's serum."
	States 
	{
		Spawn:
			TK42 ABCD 3
			Loop
		Death:
			TNT1 A 0
			Stop
	}
}

Actor Totenkopf_Tracer_Trail_Mutagen
{
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			TK57 AA 1 Bright
			Stop
	}
}
