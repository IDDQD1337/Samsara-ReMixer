Actor TotenkopfGuard : TotenkopfEnemy
{
	Health 50
	Speed 6
	PainChance 255
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	Tag "Wehrmacht Guard"
	Obituary "%o was shot by a wehrmacht guard."
	DropItem "Totenkopf_PistolEnemyDrop"
	States 
	{
		Spawn:
			TK14 A 1 A_LookEx(LOF_NOSEESOUND,0,0,0,0,"Alert")
			Loop
		Alert:
			"####" A 0 A_JumpIfTargetInLOS(2,180)
			"####" A 0 A_PlaySound("Totenkopf/GuardHear", CHAN_VOICE)
			Goto See
			"####" A 0 A_PlaySound("Totenkopf/GuardSight", CHAN_VOICE)
		See:
			"####" A 0 A_Jump(224,6)
			"####" A 0 A_JumpIfTargetInLOS(2,0)
			"####" A 0 A_JumpIf(CallACS("Totenkopf_ActiveSound",1)==1,1)
			"####" A 0
			Goto See+5
			"####" A 0 A_JumpIf(CallACS("Totenkopf_ActiveSound",0)==1,1)
			"####" A 0 A_ActiveSound
			"####" BBBCCDDDEE 2 A_Chase("Missile","Missile",CHF_NOPLAYACTIVE)
			Loop
		Missile:
			"####" A 0 A_ChangeFlag(NOPAIN,0)
			"####" G 10 A_FaceTarget
			"####" H 10 
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_Enemy",40,0,random(-2,2))
			"####" I 0 A_PlaySound("Totenkopf/atkpistol", CHAN_WEAPON)
			"####" I 10 Bright
			Goto See
		Pain:
			"####" F 5
			"####" F 5 A_Pain
			Goto See
		Death:
			"####" K 0 A_Scream
			"####" K 0 A_NoBlocking
			"####" K 7
			"####" L 8 
			"####" M 7
			"####" N -1
			Stop
		XDeath:
			TK14 A 0
			"####" O 0 A_Playsound("Totenkopf/Slurpie", CHAN_BODY)
			"####" O 0 A_NoBlocking
			"####" O 4
			"####" P 3 
			"####" Q 4
			"####" R 4
			"####" S 3 
			"####" T 4
			"####" U 3
			"####" V 4
			"####" W -1
			Stop
		Death.TotenkopfFire:
		Burn:
			TK13 A 0 A_Playsound("Totenkopf/GuardBurn", CHAN_VOICE)
			TK13 ABCDABCDABCDABCD 5 Bright
			TK13 EFG 5 Bright
			TK13 H -1
			Stop
	}
}

Actor TotenkopfGuard2 : TotenkopfGuard
{
	States 
	{
		Spawn:
			TK15 A 1 A_LookEx(LOF_NOSEESOUND,0,0,0,0,"Alert")
			Loop
	}
}

Actor TotenkopfGuardSpawner : RandomSpawner
{
	DropItem "TotenkopfGuard", 256, 1
	DropItem "TotenkopfGuard2", 256, 1
}	

Actor TotenkopfRifleman : TotenkopfGuard
{
	Tag "Wehrmacht Rifleman"
	Obituary "%o was shot by a wehrmacht rifleman."
	DropItem "Totenkopf_Kar98kEnemyDrop"
	States
	{
		Spawn:
			TK16 A 1 A_LookEx(LOF_NOSEESOUND,0,0,0,0,"Alert")
			Loop
		Missile:
			"####" A 0 A_ChangeFlag(NOPAIN,0)
			"####" G 10 A_FaceTarget
			"####" H 10 
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_Rifleman",40,0,random(-1,1))
			"####" I 0 A_PlaySound("Totenkopf/atkkar98", CHAN_WEAPON)
			"####" I 10 Bright
			Goto See
	}
}

Actor Totenkopf_Tracer_Rifleman : Totenkopf_Tracer_Enemy 
{ 
	Damage (10+(random(0,256)%10)) 
	States
	{
		Death:
		Crash:
			TNT1 A 0 A_Playsound("Totenkopf/Ricochet", CHAN_BODY)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_Tracer",1,24,40) == 1, 1)
			TK51 BCDE 2 Bright
			Stop
		XDeath:
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_Tracer",1,24,40) == 1, 1)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 1 Bright
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1
			Stop
	}
}

Actor TotenkopfInfantry : TotenkopfGuard
{
	Tag "Wehrmacht Infantry"
	Obituary "%o was shot by a wehrmacht infantry."
	DropItem "Totenkopf_Mp40EnemyDrop"
	States
	{
		Spawn:
			TK17 A 1 A_LookEx(LOF_NOSEESOUND,0,0,0,0,"Alert")
			Loop
		Missile:
			"####" A 0 A_ChangeFlag(NOPAIN,0)
			"####" G 10
			"####" H 8 A_FaceTarget
		MissileHold:
			"####" H 2
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_Infantry",40,0,random(-3,3))
			"####" I 0 A_PlaySound("Totenkopf/atkmp40", CHAN_WEAPON)
			"####" I 2 Bright
			"####" H 3
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_Infantry",40,0,random(-3,3))
			"####" I 0 A_PlaySound("Totenkopf/atkmp40", CHAN_WEAPON)
			"####" I 3 Bright
			"####" H 2
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_Infantry",40,0,random(-3,3))
			"####" I 0 A_PlaySound("Totenkopf/atkmp40", CHAN_WEAPON)
			"####" I 3 Bright
			"####" H 0 A_MonsterRefire(0,"See")
			Loop
	}
}

Actor TotenkopfInfantry2 : TotenkopfInfantry
{
	States
	{
		Spawn:
			TK18 A 1 A_LookEx(LOF_NOSEESOUND,0,0,0,0,"Alert")
			Loop
	}
}

Actor TotenkopfInfantrySpawner : RandomSpawner
{
	DropItem "TotenkopfInfantry", 256, 1
	DropItem "TotenkopfInfantry2", 256, 1
}	

Actor Totenkopf_Tracer_Infantry : Totenkopf_Tracer_Enemy 
{ 
	Damage (3+(random(0,256)%10)) 
	States
	{
		Death:
		Crash:
			TNT1 A 0 A_Playsound("Totenkopf/Ricochet", CHAN_BODY)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_Tracer",1,24,40) == 1, 1)
			TK51 BCDE 2 Bright
			Stop
		XDeath:
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_Tracer",1,24,40) == 1, 1)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 1 Bright
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1
			Stop
	}
}

Actor TotenkopfOfficer : TotenkopfGuard
{
	Tag "Wehrmacht Officer"
	Obituary "%o was shot by a wehrmacht officer."
	States 
	{
		Spawn:
			TK19 A 1 A_LookEx(LOF_NOSEESOUND,0,0,0,0,"Alert")
			Loop
		See:
			"####" A 0 A_Jump(224,6)
			"####" A 0 A_JumpIfTargetInLOS(2,0)
			"####" A 0 A_JumpIf(CallACS("Totenkopf_ActiveSound",2)==1,1)
			"####" A 0
			Goto See+5
			"####" A 0 A_JumpIf(CallACS("Totenkopf_ActiveSound",0)==1,1)
			"####" A 0 A_ActiveSound
			"####" BBBCCDDDEE 2 A_Chase("Missile","Missile",CHF_NOPLAYACTIVE)
			Loop
		Missile:
			"####" A 0 A_ChangeFlag(NOPAIN,0)
			"####" G 10 A_FaceTarget
			"####" H 10 
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_Officer",40,0,random(-1,1))
			"####" I 0 A_PlaySound("Totenkopf/atkpistol", CHAN_WEAPON)
			"####" I 10 Bright
			Goto See
	}
}

Actor TotenkopfOfficer2 : TotenkopfInfantry
{
	Tag "Wehrmacht Officer"
	Obituary "%o was shot by a wehrmacht officer."
	States 
	{
		Spawn:
			TK20 A 1 A_LookEx(LOF_NOSEESOUND,0,0,0,0,"Alert")
			Loop
		See:
			"####" A 0 A_Jump(224,6)
			"####" A 0 A_JumpIfTargetInLOS(2,0)
			"####" A 0 A_JumpIf(CallACS("Totenkopf_ActiveSound",2)==1,1)
			"####" A 0
			Goto See+5
			"####" A 0 A_JumpIf(CallACS("Totenkopf_ActiveSound",0)==1,1)
			"####" A 0 A_ActiveSound
			"####" BBBCCDDDEE 2 A_Chase("Missile","Missile",CHF_NOPLAYACTIVE)
			Loop
	}
}

Actor TotenkopfOfficerSpawner : RandomSpawner
{
	DropItem "TotenkopfOfficer", 256, 1
	DropItem "TotenkopfOfficer2", 256, 1
}	

Actor Totenkopf_Tracer_Officer : Totenkopf_Tracer_Enemy { Damage (4+(random(0,256)%12)) }

		