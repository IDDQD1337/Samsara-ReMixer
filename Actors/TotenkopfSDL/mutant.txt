Actor TotenkopfMutant : TotenkopfEnemy
{
	Health 150
	Speed 6
	PainChance 255
	+FLOORCLIP
    +NOTDMATCH
    +THRUGHOST
	+MISSILEMORE
	-NOINFIGHTING
	Tag "Mutant"
	SeeSound ""
	ActiveSound ""
	DeathSound "Totenkopf/MutantDeath"
	Obituary "%o was shot by a mutant."
	DropItem "Totenkopf_MP40EnemyDrop"
	BloodColor "94 0 96"
	BloodType "TotenkopfBloodMutant"
	States 
	{
		Spawn:
			TK35 A 1 A_Look
			Loop
		See:
			"####" BBBCCDDDEE 2 A_Chase
			Loop
		Missile:
			"####" A 0 A_ChangeFlag(NOPAIN,0)
			"####" G 5 A_FaceTarget
		MissileHold:
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_Mutant",44,0,random(-3,3))
			"####" I 0 A_PlaySound("Totenkopf/MutantFire", CHAN_WEAPON)
			"####" I 2 Bright A_FaceTarget
			"####" H 0 A_CustomMissile("Totenkopf_Tracer_Mutant",44,0,random(-3,3))
			"####" H 3 Bright A_FaceTarget
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_Mutant",44,0,random(-3,3))
			"####" I 2 Bright A_FaceTarget
			"####" G 3
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_Mutant",44,0,random(-3,3))
			"####" I 0 A_PlaySound("Totenkopf/MutantFire", CHAN_WEAPON)
			"####" I 2 Bright A_FaceTarget
			"####" J 0 A_CustomMissile("Totenkopf_Tracer_Mutant",44,0,random(-3,3))
			"####" J 2 Bright A_FaceTarget
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_Mutant",44,0,random(-3,3))
			"####" I 2 Bright A_FaceTarget
			"####" G 3
			"####" G 0 A_MonsterRefire(0,"See")
			Loop
		Pain:
			"####" F 5
			"####" F 5 A_Pain
			Goto See
		Death:
			"####" K 0 A_Scream
			"####" K 0 A_NoBlocking
			"####" K 7
			"####" L 8 
			"####" M 7
			"####" N 8
			"####" O -1
			Stop
		XDeath:
			TK35 A 0
			"####" P 0 A_Playsound("Totenkopf/Slurpie", CHAN_BODY)
			"####" P 0 A_NoBlocking
			"####" P 4
			"####" Q 3 
			"####" R 4
			"####" S 4
			"####" T 3 
			"####" U 4
			"####" V 3
			"####" W 4
			"####" X -1
			Stop
		Death.TotenkopfFire:
		Burn:
			TK13 A 0 A_Scream
			TK13 ABCDABCDABCDABCD 5 Bright
			TK13 EFG 5 Bright
			TK13 H -1
			Stop
	}
}

Actor TotenkopfMutant2 : TotenkopfMutant
{
	States
	{
		Spawn:
			TK36 A 1 A_Look
			Loop
	}
}

Actor TotenkopfMutantSpawner : RandomSpawner
{
	DropItem "TotenkopfMutant", 256, 1
	DropItem "TotenkopfMutant2", 256, 1
}	

Actor Totenkopf_Tracer_Mutant : Totenkopf_Tracer_Enemy 
{ 
	Damage (2+(random(0,256)%10)) 
	States
	{
		Death:
		Crash:
			TNT1 A 0 A_Playsound("Totenkopf/Ricochet", CHAN_BODY)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_Tracer",1,24,44) == 1, 1)
			TK51 BCDE 2 Bright
			Stop
		XDeath:
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_Tracer",1,24,44) == 1, 1)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 1 Bright
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1
			Stop
	}
}