Actor TotenkopfWaffenSS : TotenkopfSS
{
	Health 200
	Tag "Waffen SS"
	Obituary "%o was shot by a Waffen SS."
	DropItem "Totenkopf_STG44EnemyDrop"
	States
	{
		Spawn:
			TK26 A 1 A_LookEx(LOF_NOSEESOUND,0,0,0,0,"Alert")
			Loop
		Missile:
			"####" A 0 A_ChangeFlag(NOPAIN,0)
			"####" G 10
			"####" H 8 A_FaceTarget
		MissileHold:
			"####" H 2
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_WaffenSS",40,0,random(-1,1))
			"####" I 0 A_PlaySound("Totenkopf/atkstg44", CHAN_WEAPON)
			"####" I 2 Bright
			"####" H 3
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_WaffenSS",40,0,random(-1,1))
			"####" I 0 A_PlaySound("Totenkopf/atkstg44", CHAN_WEAPON)
			"####" I 3 Bright
			"####" H 2
			"####" I 0 A_CustomMissile("Totenkopf_Tracer_WaffenSS",40,0,random(-1,1))
			"####" I 0 A_PlaySound("Totenkopf/atkstg44", CHAN_WEAPON)
			"####" I 3 Bright
			"####" H 0 A_MonsterRefire(0,"See")
			Loop
		XDeath:
			TK26 A 0
			"####" O 0 A_Playsound("Totenkopf/Slurpie", CHAN_BODY)
			"####" O 0 A_NoBlocking
			"####" O 4
			"####" P 3 
			"####" Q 4
			"####" R 4
			"####" S 3 
			"####" T 4
			"####" U 3
			"####" V 4
			"####" W -1
			Stop
	}
}

Actor TotenkopfWaffenSS2 : TotenkopfWaffenSS
{
	States
	{
		Spawn:
			TK27 A 1 A_LookEx(LOF_NOSEESOUND,0,0,0,0,"Alert")
			Loop
	}
}

Actor TotenkopfWaffenSS3 : TotenkopfWaffenSS
{
	States
	{
		Spawn:
			TK28 A 1 A_LookEx(LOF_NOSEESOUND,0,0,0,0,"Alert")
			Loop
	}
}

Actor TotenkopfWaffenSSOfficer : TotenkopfWaffenSS
{
	Tag "Waffen SS Officer"
	Obituary "%o was shot by a Waffen SS Officer."
	States
	{
		Spawn:
			TK29 A 1 A_LookEx(LOF_NOSEESOUND,0,0,0,0,"Alert")
			Loop
		See:
			"####" A 0 A_Jump(224,6)
			"####" A 0 A_JumpIfTargetInLOS(2,0)
			"####" A 0 A_JumpIf(CallACS("Totenkopf_ActiveSound",2)==1,1)
			"####" A 0
			Goto See+5
			"####" A 0 A_JumpIf(CallACS("Totenkopf_ActiveSound",0)==1,1)
			"####" A 0 A_ActiveSound
			"####" BBBCCDDDEE 2 A_Chase("Missile","Missile",CHF_NOPLAYACTIVE)
			Loop
	}
}

Actor TotenkopfWaffenSSSpawner : RandomSpawner
{
	DropItem "TotenkopfWaffenSS", 256, 1
	DropItem "TotenkopfWaffenSS2", 256, 1
	DropItem "TotenkopfWaffenSSOfficer", 256, 1
}	

Actor Totenkopf_Tracer_WaffenSS : Totenkopf_Tracer_Enemy 
{ 
	Damage (7+(random(0,256)%10)) 
	States
	{
		Death:
		Crash:
			TNT1 A 0 A_Playsound("Totenkopf/Ricochet", CHAN_BODY)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_Tracer",1,24,40) == 1, 1)
			TK51 BCDE 2 Bright
			Stop
		XDeath:
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_Tracer",1,24,40) == 1, 1)
			TNT1 A 0 A_JumpIf(CallACS("Totenkopf_BloodGenerator_Offsets")==1,1)
			TNT1 A 1 Bright
			TNT1 A 0 A_GiveInventory("Totenkopf_BloodGenerator",1,AAPTR_TRACER)
			TNT1 A 1
			Stop
	}
}
		