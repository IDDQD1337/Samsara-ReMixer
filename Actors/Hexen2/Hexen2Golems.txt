Actor Hexen2StoneGolem
{
	Health 200
	Radius 24
	Height 64
	Mass 750
	Speed 10
	+NODROPOFF
	+NOBLOOD
	+NOICEDEATH
	Monster
	PainChance 12
	Species "Enemies"
	Obituary "%o was ROCKED by a Stone Golem."
	SeeSound "Hexen2/Golem/See"
	PainSound "Hexen2/Golem/Stone/Pain"
	DeathSound "Hexen2/Golem/Death"
	MeleeRange 132
	MeleeThreshold 500
	States
	{
	Spawn:
		HX00 A 0
		HX00 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
		HX00 A 1 ACS_ExecuteAlways(668,0,1,0)
		HX00 A 0 A_GiveInventory("AlreadySpawned",1)
	Spawn2:		
		HX00 ABCDEFGHIJKLMNOPQRSTUV 2 A_Look
		Loop
	Idle:
		HX01 ABCDEFGHIJKL 2 A_Chase
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		HX03 ABCDEFGHIJKLMNOPQ 2 A_Chase
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		HX04 ABCDEFGHIJIJKLMNOP 2 A_Chase
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		Loop
	See:
		TNT1 A 0 A_JumpIfCloser(250,"Melee")
		TNT1 A 0 A_JumpIfCloser(350,"Slide")
		HX05 ACDEFGHIJKLM 2 A_Chase
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		TNT1 A 0 A_JumpIfCloser(250,"Melee")
		TNT1 A 0 A_JumpIfCloser(350,"Slide")
		HX05 NOPQRSTUVWXYZ 2 A_Chase
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		TNT1 A 0 A_JumpIfCloser(250,"Melee")
		TNT1 A 0 A_JumpIfCloser(350,"Slide")
		Loop
	Pain:
		HX02 ABCDEFGH 2 A_FaceTarget
		HX02 A 0 A_Pain
		HX02 IJKLMNO 2 A_FaceTarget
		Goto See
	Melee:
		TNT1 AAAAAAAAAA 0 A_FaceTarget
		TNT1 A 0 A_Jump(85,"SlamAtk")
		TNT1 A 0 A_Jump(48,"Slide")
	Punch:	
		HX06 ABCDEFGHIJ 2 A_Recoil(-1.00)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		TNT1 A 0 A_CustomMeleeAttack(random(5,22),"Hexen2/Golem/Hit", "Hexen2/Golem/Swing", "Hexen2Damage", True)
		TNT1 AAAAAAAAAAAAAA 0 A_FaceTarget
		HX06 KLMNOPQRSTUVWX 2 A_Recoil(-1.00)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		Goto See
	SlamAtk:
		HX07 ABCDEFGHIJK 2 A_Recoil(-1.00)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		TNT1 A 0 A_CustomMeleeAttack(random(5,22),"Hexen2/Golem/Hit", "Hexen2/Golem/Swing", "Hexen2Damage", True)	
		TNT1 AAAAAAAAAAAA 0 A_FaceTarget
		HX07 LMNOPQRSTUVW 2 A_Recoil(-1.00)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		Goto See
	Slide:
		HX09 ABCDEFGHIJ 2 A_Recoil(-1.00)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		HX09 KLMNO 2 A_Recoil(-2.50)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Slide", CHAN_BODY,1.5)
		TNT1 A 0 A_CustomMeleeAttack(random(20,30),"Hexen2/Golem/Hit", "", "Hexen2Damage", True)	
		HX09 PQRSTUVW 2 A_Recoil(-2.50)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		Goto See
	Death:
		TNT1 A 0 A_JumpIfHealthLower(-50, "XDeath")
		HX08 ABCDEFGHIJ 2 A_FaceTarget
		TNT1 A 0 A_Scream
		HX08 KLMNOPQ 8
		HX08 RSTUV 2
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("Hexen2StoneGibsSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 AA 0 A_SpawnItemEx("Hexen2StoneGolemArm",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("Hexen2StoneGolemHead",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("Hexen2StoneGolemLegs",0,0,0,velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERSCALE|SXF_ABSOLUTEVELOCITY)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Stone/Fall", CHAN_BODY,1.5)
		TNT1 A 5 A_FadeOut
		Stop
	XDeath:
		HX08 ABCDEFGHIJ 2 A_FaceTarget
		TNT1 A 0 A_Scream
		HX08 KLMNOPQ 8		
		TNT1 A 0 A_NoBlocking
		TNT1 AA 0 A_SpawnItemEx("Hexen2StoneGibsSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Stone/Fall", CHAN_BODY,1.5)
		TNT1 A 5 A_FadeOut	
		Stop
		}
}	

Actor Hexen2StoneGolemLegs : Hexen2CorpseActor
{
	-SOLID
	-SHOOTABLE
	-VULNERABLE
	+NOBLOOD
	Height 16
	Radius 21
	States
	{
	Spawn:
		HX00 A -1		
		Loop
	}
}
Actor Hexen2IronGolemLegs : Hexen2StoneGolemLegs { }
Actor Hexen2BronzeGolemLegs : Hexen2StoneGolemLegs { }

Actor Hexen2StoneGolemArm : Hexen2StoneGolemLegs { Radius 10 }
Actor Hexen2IronGolemArm :  Hexen2StoneGolemArm { }
Actor Hexen2BronzeGolemArm : Hexen2StoneGolemArm { }

Actor Hexen2StoneGolemHead : Hexen2HeadActor
{
	-SOLID
	-SHOOTABLE
	-VULNERABLE
	+NOBLOOD
	States
	{
	Spawn:
		HX00 A -1		
		Loop
	}
}
Actor Hexen2IronGolemHead : Hexen2StoneGolemHead { }
Actor Hexen2BronzeGolemHead : Hexen2StoneGolemHead { }

Actor Hexen2StoneChunk1 : Hexen2Chunk1
{
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_SetScale(scalex+frandom(0.0,0.1))
		SpawnLoop:
			HX00 A 0 A_SetAngle(angle+12)
			HX00 A 0 A_SetPitch(angle-12)
			HX00 A 1
			Loop
		Death:
		DeathLoop:
			HX00 A 0 A_SetAngle(0)
			HX00 A 0 A_SetPitch(0)
		    HX00 A 70
			stop
		Remove:
			TNT1 A 1
			stop
			}
}

Actor Hexen2StoneChunk2 : Hexen2StoneChunk1 { }
Actor Hexen2StoneChunk3 : Hexen2StoneChunk1 { }		
Actor Hexen2MetalChunk1 : Hexen2StoneChunk1 { }
Actor Hexen2MetalChunk2 : Hexen2StoneChunk1 { }
Actor Hexen2MetalChunk3 : Hexen2StoneChunk1 { }
Actor Hexen2MetalChunk4 : Hexen2StoneChunk1 { }

Actor Hexen2StoneGibsSpawner : Hexen2GibSpawner 
{
	States
	{
		Spawn:
			TNT1 A 0	
			TNT1 A 0 A_SpawnItemEx("Hexen2PuffGenerator",random(-6,6),random(-6,6),random(-4,4),0,0,frandom(-8,8),0,SXF_NOCHECKPOSITION)
			TNT1 A 0 A_SetUserVar("user_count", CallACS("Hexen2_Decorate",25))
		SpawnLoop:
			TNT1 A 0 A_JumpIfInventory("Hexen2GibCounter",user_count,"SpawnRemove")
			TNT1 A 0 A_GiveInventory("Hexen2GibCounter",1)
			TNT1 A 0 A_Jump(256,"Chunk1","Chunk2","Chunk3")
		Chunk1:
			TNT1 A 0 A_SpawnItemEx("Hexen2StoneChunk1",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop
		Chunk2:
			TNT1 A 0 A_SpawnItemEx("Hexen2StoneChunk2",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop
		Chunk3:
			TNT1 A 0 A_SpawnItemEx("Hexen2StoneChunk3",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop
		SpawnRemove:
			TNT1 A 1
			Stop
	}
}

Actor Hexen2IronGolem : Hexen2StoneGolem
{
	Health 450
	HitObituary "%o was PUNKED by an Iron Golem."
	Obituary "%o was Vaporized by an Iron Golem."
	PainSound "Hexen2/Golem/Metal/Pain"
	Height 70
	MinMissileChance 2400
	MaxTargetRange 1000
	DamageFactor "Tesla", 2.0
	DamageFactor "MarathonFusion", 2.0
	DamageFactor "ElectricBolts", 2.0
	States
	{
	See:
		TNT1 A 0 A_JumpIfCloser(250,"Melee")
//		TNT1 A 0 A_JumpIfCloser(750,"Laser")
		HX05 ACDEFGHIJKLM 2 A_Chase
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		TNT1 A 0 A_JumpIfCloser(250,"Melee")
//		TNT1 A 0 A_JumpIfCloser(750,"Laser")
		HX05 NOPQRSTUVWXYZ 2 A_Chase
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		TNT1 A 0 A_JumpIfCloser(250,"Melee")
//		TNT1 A 0 A_JumpIfCloser(750,"Laser")
		Loop
	Missile:
		HX09 ABCDEFGHIJKLMNOP 2 A_FaceTarget
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Fire", CHAN_BODY,1.5)
		TNT1 A 0 A_CustomMissile("Hexen2GolemMissile",64,0,0)	
		HX09 PQRSTUVW 2 
		Goto See
	Slide:
		Goto see	
	Death:
		TNT1 A 0 A_JumpIfHealthLower(-50, "XDeath")	
		HX08 ABCDEFGHIJ 2 A_FaceTarget
		TNT1 A 0 A_Scream
		HX08 KLMNOPQ 8
		HX08 RSTUV 2
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("Hexen2MetalGibsSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 AA 0 A_SpawnItemEx("Hexen2IronGolemArm",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("Hexen2IronGolemHead",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("Hexen2IronGolemLegs",0,0,0,velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERSCALE|SXF_ABSOLUTEVELOCITY)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Metal/Fall", CHAN_BODY,1.5)
		TNT1 A 5 A_FadeOut
		Stop	
	XDeath:
		HX08 ABCDEFGHIJ 2 A_FaceTarget
		TNT1 A 0 A_Scream
		HX08 KLMNOPQ 8		
		TNT1 A 0 A_NoBlocking
		TNT1 AA 0 A_SpawnItemEx("Hexen2MetalGibsSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Metal/Fall", CHAN_BODY,1.5)
		TNT1 A 5 A_FadeOut	
		Stop
		}
}		

Actor Hexen2MetalGibsSpawner : Hexen2StoneGibsSpawner
{
	States
	{
		SpawnLoop:
			TNT1 A 0 A_JumpIfInventory("Hexen2GibCounter",user_count,"SpawnRemove")
			TNT1 A 0 A_GiveInventory("Hexen2GibCounter",1)
			TNT1 A 0 A_Jump(256,"Chunk1","Chunk2","Chunk3","Chunk4")
		Chunk1:
			TNT1 A 0 A_SpawnItemEx("Hexen2MetalChunk1",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop
		Chunk2:
			TNT1 A 0 A_SpawnItemEx("Hexen2MetalChunk2",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop
		Chunk3:
			TNT1 A 0 A_SpawnItemEx("Hexen2MetalChunk3",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop
		Chunk4:
			TNT1 A 0 A_SpawnItemEx("Hexen2MetalChunk4",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
			Goto SpawnLoop			
			}
}			

Actor Hexen2GolemMissile
{
	Speed 3
	Radius 3
	Height 5
	Projectile
	Damage (random(7,17))
	+SEEKERMISSILE
	DeathSound ""
	DamageType "Hexen2Damage"
	States
	{
	Spawn:
		HX00 A 0 BRIGHT A_SeekerMissile(5,10,SMF_LOOK,SMF_PRECISE)
		HX00 A 8 BRIGHT A_SpawnItemEx("Hexen2GemPuffGenerator", frandom(-16,16),frandom(-16,16),frandom(-16,16),0,0,0,0,SXF_NOCHECKPOSITION)
		Loop
	Death:
		TNT1 A 0
		Stop
		}
}		
		
Actor Hexen2GemPuffGenerator : Hexen2PuffGenerator { Translation "0:255=%[0.0,0.0,0.0]:[0.713, 0.12, 0.12]" }	

Actor Hexen2BronzeGolem : Hexen2IronGolem 
{
	Health 650
	States
	{	
	Stomp:
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		HX11 ABCDEFGHIJKLMNO 2 A_Recoil(-1.50)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Stomp", CHAN_BODY,1.5)
		TNT1 A 0 Radius_Quake(2,20,5,5,0)
		HX11 PQRSTUVWX 2 A_Recoil(-1.50)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		Goto See	
	Melee:
		TNT1 AAAAAAAAAA 0 A_FaceTarget
		TNT1 A 0 A_Jump(85,"SlamAtk")
		TNT1 A 0 A_Jump(85,"Stomp")
	Punch:	
		HX06 ABCDEFGHIJ 2 A_Recoil(-1.00)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		TNT1 A 0 A_CustomMeleeAttack(random(5,22),"Hexen2/Golem/Hit", "Hexen2/Golem/Swing", "Hexen2Damage", True)
		TNT1 AAAAAAAAAAAAAA 0 A_FaceTarget
		HX06 KLMNOPQRSTUVWX 2 A_Recoil(-1.00)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		Goto See
	SlamAtk:
		HX07 ABCDEFGHIJK 2 A_Recoil(-1.00)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		TNT1 A 0 A_CustomMeleeAttack(random(5,22),"Hexen2/Golem/Hit", "Hexen2/Golem/Swing", "Hexen2Damage", True)	
		TNT1 AAAAAAAAAAAA 0 A_FaceTarget
		HX07 LMNOPQRSTUVW 2 A_Recoil(-1.00)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Step", CHAN_BODY,1.5)
		Goto See
	Missile:
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Charge", CHAN_BODY,1.5)
		HX09 ABCDEFGHIJKLMNOP 3 A_FaceTarget
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Fire", CHAN_BODY,1.5)
		TNT1 A 0 A_CustomMissile("Hexen2GolemMissile",64,0,0)	
		HX09 PQRSTUVW 2 
		Goto See		
	Death:
		TNT1 A 0 A_JumpIfHealthLower(-50, "XDeath")	
		HX08 ABCDEFGHIJ 2 A_FaceTarget
		TNT1 A 0 A_Scream
		HX08 KLMNOPQ 8
		HX08 RSTUV 2
		TNT1 A 0 A_NoBlocking
		TNT1 A 0 A_SpawnItemEx("Hexen2MetalGibsSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 AA 0 A_SpawnItemEx("Hexen2BronzeGolemArm",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("Hexen2BronzeGolemHead",0,0,0,random(-5,5),random(-5,5),frandom(2,6),Random(1,360),SXF_TRANSFERSCALE)
		TNT1 A 0 A_SpawnItemEx("Hexen2BronzeGolemLegs",0,0,0,velx, vely, velz, 0, SXF_NOCHECKPOSITION|SXF_SETMASTER|SXF_TRANSFERSCALE|SXF_ABSOLUTEVELOCITY)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Metal/Fall", CHAN_BODY,1.5)
		TNT1 A 0 A_KillMaster
		TNT1 A 5 A_FadeOut
		Stop	
	XDeath:
		HX08 ABCDEFGHIJ 2 A_FaceTarget
		TNT1 A 0 A_Scream
		HX08 KLMNOPQ 8		
		TNT1 A 0 A_NoBlocking
		TNT1 AA 0 A_SpawnItemEx("Hexen2MetalGibsSpawner",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_SETMASTER)
		TNT1 A 0 A_PlaySound("Hexen2/Golem/Metal/Fall", CHAN_BODY,1.5)
		TNT1 A 0 A_KillMaster
		TNT1 A 5 A_FadeOut	
		Stop	
		}
}	

/*Actor Hexen2GolemQuake
{
	Radius 1
	Height 1
	Projectile
	Speed 0
	DamageType "Hexen2Damage"
	Species "Enemies"
	+NODROPOFF
	+THRUSPECIES
	+DONTHURTSPECIES
	States
	{
	Spawn:
		TNT1 A 1 A_Explode(50,350)
		TNT1 A 0
		Goto Death
	Death:
		TNT1 A -1
		Stop
		}
}		*/
