ACTOR LostSoul64 : SamsaraMonsterSkeleton
{
	Health 60
	Radius 16
	Height 56
	Mass 50
	RenderStyle "Normal" // gzdoom
	+MISSILEMORE
	+MISSILEEVENMORE
	+NOBLOOD
	+NOBLOODDECALS
	+NOTARGET
	+NOICEDEATH
	+DONTFALL
	+FLOAT
	+NOGRAVITY
	Species "SamsaraLostSoul"
	Tag "Lost Soul"
	Speed 8
	Damage 3
	PainChance 256
	Monster
	Scale 0.8
	AttackSound "64skull/melee"
	PainSound "64demon/pain"
	DeathSound "doom64guy/plasmax"
	ActiveSound "64demon/active"
	Obituary "%o was blown up by a lost soul."
	HitObituary "$OB_SKULL"
	DamageFactor "LostSoul64Explosion", 0
	DamageFactor "Mace", 1.1
	DamageFactor "DragonClaw", 1.5
	DamageFactor "BlakeEnemyBullet", 0.0
	DamageFactor "PoisonBolt", 0.5
	PainChance "MightyBoot", 255
	PainChance "MarathonStun", 255
	PainChance "CalebFire", 0
	PainChance "WTMelee", 255
	PainChance "WTMeleeIce", 255
	PainChance "ShrinkerDMG", 0
	DamageFactor "WitchMelee", 1.1
	States
	{
		Spawn:
			64LS A 0 bright
			64LS A 1 bright A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			64LS A 1 bright ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			64LS A 0 bright A_GiveInventory("AlreadySpawned",1)
			64LS A 0 bright A_GiveInventory("IsAFlyingActor")
			64LS A 0 bright
		Spawn2:
			64LS ABC 5 bright A_Look
			Loop
		See:
			64LS ABC 3 bright A_Chase
			Loop
		Missile:
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			64LS D 6 bright A_FaceTarget
			64LS E 0 bright A_ChangeFlag("NOTARGET",0)
			64LS E 4 bright A_SkullAttack(34)
			64LS DE 4 bright
			Goto Missile+4
		Pain:
			64LS F 3 bright
			64LS F 3 bright A_Pain
			Goto See
		Pain.Vertigo:
			64LS F 0 bright ThrustThingZ(0, 55, 0, 0)
			Goto Pain
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySound("duke/kickhit", CHAN_BODY)
			64LS F 2 bright A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			64LS F 16 bright A_Pain
		Death:
			64LS G 6 bright ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			64LS H 6 bright A_Scream
			64LS I 4 bright
			64LS J 3 bright A_NoBlocking
			64LS KLMNOP 3 bright A_FadeOut(0.2)
			Stop
		Crush:
			TNT1 A 1
			Stop
	}
}

ACTOR LostSoul64PE : LostSoul64
{
	var int user_lostsoul_spawned;
	States
	{
		Spawn:
			64LS A 0 bright
			64LS A 0 bright A_SetUserVar("user_lostsoul_spawned", 1)
			64LS A 1 bright A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			64LS A 0 bright A_GiveInventory("MultiplyOverflowToken", 1)
			Goto Super::Spawn+2
		See:
			64LS A 0 bright A_SetUserVar("user_lostsoul_spawned", 1)
			Goto Super::See
		Pain:
			64LS F 0 bright A_SetUserVar("user_lostsoul_spawned", 1)
			Goto Super::Pain
		Pain.Vertigo:
			64LS F 0 bright A_SetUserVar("user_lostsoul_spawned", 1)
			Goto Super::Pain.Vertigo
		Pain.MightyBoot:
			64LS F 0 bright A_SetUserVar("user_lostsoul_spawned", 1)
			Goto Super::Pain.MightyBoot
		Death:
			TNT1 A 0 A_JumpIf(user_lostsoul_spawned == 0, "DeathExplode")
			Goto Super::Death
		DeathExplode:
			64LS G 0 bright A_Stop
			64LS G 6 bright ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			64LS H 0 bright A_SpawnItemEx("LostSoul64DeathExplosion",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION)
			Goto Super::Death+1
	}
}

ACTOR LostSoul64DeathExplosion
{
	DamageType "LostSoul64Explosion"
	+NOINTERACTION
	+NODAMAGETHRUST
	States
	{
		Spawn:
			TNT1 A 0
			TNT1 A 0 A_Explode(128, 128, 0)
			Stop
	}
}