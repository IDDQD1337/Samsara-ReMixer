ACTOR MotherDemon64
{
	Health 6000
	Radius 40
	Height 110
	Mass 1000
	Speed 18
	PainChance 10
	ReactionTime 8
	Species "Enemies"	
	Tag "Mother Demon"
	SeeSound "64mother/sight"
	AttackSound "64mother/attack"
	PainSound "64mother/pain"
	DeathSound "64mother/death"
	ActiveSound "64mother/active"
	Obituary "%o was obliterated by the mother of all demons."
	MONSTER
	Scale 0.80
	+FLOORCLIP
	+DONTMORPH
	+NORADIUSDMG
	+MISSILEMORE
	+MISSILEEVENMORE
	+NOFEAR
	+BOSS	
	+BOSSDEATH
	+NOINFIGHTING
    BloodType "Doom64Blood"	
	PainChance "PhosFireCoop", 0 	
	PainChance "EradCoopGreenFire", 0
	PainChance "EradGreenFire", 0
	PainChance "StrifeExplosive", 0
	PainChance "PSFire", 5	
	PainChance "WTMelee", 96
	PainChance "WTMeleeIce", 96	
	PainChance "ShrinkerDMG", 0
	DamageFactor "Shock", 0.5	
	DamageFactor "PhosFireCoop", 0.6		
	DamageFactor "EradGreenFire", 0.7
	DamageFactor "EradCoopGreenFire", 0.7
	DamageFactor "Tesla", 1.0
	DamageFactor "ElectricBolt", 2.0
	DamageFactor "PoisonBolt", 0.5		
	DamageFactor "RottFire", 0.85
	DamageFactor "RottPistolBullet", 0.9
	DamageFactor "StrifeExplosive", 0.5	
	DamageFactor "DisruptorWall", 0.6	
	DamageFactor "WitchMelee", 1.1
	DamageFactor "WTMelee", 1.5
	DamageFactor "PGBullet", 1.25
	DamageFactor "PGPlasma", 1.25
	DamageFactor "PGLaser", 1.25
	DamageFactor "PGFire", 1.25
	DamageFactor "PGExplosive", 1.25			
	DamageFactor "Mace", 1.1
    DamageFactor "DragonClaw", 1.5
	DamageFactor "BlakeEnemyBullet", 0.0
	States
	{
		Spawn:
			64MD A 0
			64MD A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			64MD A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			64MD A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			64MD ABCD 13 A_Look
			Loop
		See:
			TNT1 A 0 A_JumpIfInventory("SawPlayer", 1, "See2")
			TNT1 A 0 A_GiveInventory("SawPlayer", 1)		
			TNT1 A 0 A_GiveToTarget("EleenaBossSightTaunt")
			TNT1 A 0 A_GiveToTarget("LoWangBossSightTaunt")
			TNT1 A 0 A_GiveToTarget("LeonardBossSightTaunt")					
			TNT1 A 0 A_GiveToTarget("DukeBossSightTaunt")
			TNT1 A 0 A_GiveToTarget("CalebBossSightTaunt")
			TNT1 A 0 A_GiveToTarget("HLBossSightMusicQueue")
		See2:	
			64MD AA 3 A_Chase
			64MD A 3 A_Chase("","")
			64MD BB 3 A_Chase
			64MD B 3 A_Chase("","")
			64MD CC 3 A_Chase
			64MD C 3 A_Chase("","")
			64MD DD 3 A_Chase
			64MD D 3 A_Chase("","")
			Loop
		Missile:
			64MD E 8 A_FaceTarget
			64MD E 0 A_PlaySoundEx("64mother/attack", "BODY")
			64MD G 0 BRIGHT A_CustomMissile("MotherFire64", 0, 0, 0, 2, 0)
			64MD G 0 BRIGHT A_CustomMissile("MotherFire64", 0, 0, 90, 2, 0)
			64MD G 0 BRIGHT A_CustomMissile("MotherFire64", 0, 0, -90, 2, 0)
			64MD G 14 BRIGHT A_CustomMissile("MotherFire64", 0, 0, 180, 2, 0)
			64MD E 8 A_FaceTarget //              
			64MD F 0 BRIGHT A_CustomMissile("MotherBall64", 144, -25, 90) 
			64MD F 0 BRIGHT A_CustomMissile("MotherBall64", 72, -50, 90) 
			64MD F 0 BRIGHT A_CustomMissile("MotherBall64", 144, 25, -90)
			64MD F 14 BRIGHT A_CustomMissile("MotherBall64", 72, 50, -90)
			64MD E 8
			Goto See
		Pain:
			64MD H 10 A_Pain
			Goto See
		Pain.Vertigo:
			64MD H 0 ThrustThingZ(0, 5, 0, 0)
			64MD H 10
			64MD H 10 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)		
			64MD I 10
			64MD I 0 A_Scream
			64MD I 4 A_SpawnItemEx("BossExplosion64", 0, 0, 0, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 0, 0, 0, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", -20, -20, 55, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 20, 20, 110, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 20, 20, 0, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 0, 0, 55, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 0, 0, 110, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", -20, -20, 0, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 20, 20, 55, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", -20, -20, 110, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 0, 0, 0, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", -20, -20, 55, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", -20, -20, 110, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 20, 20, 0, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 20, 20, 55, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 0, 0, 110, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 20, 20, 0, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 0, 0, 55, 0, 0, 0, 0, 130)
			64MD I 4 A_SpawnItemEx("BossExplosion64", 20, 20, 110, 0, 0, 0, 0, 130)
			64MD J 4 A_SpawnItemEx("BossExplosion64", -20, -20, 0, 0, 0, 0, 0, 130)
			64MD J 4 A_SpawnItemEx("BossExplosion64", -20, -20, 55, 0, 0, 0, 0, 130)
			64MD K 4 A_SpawnItemEx("BossExplosion64", 20, 20, 110, 0, 0, 0, 0, 130)
			64MD K 4 A_SpawnItemEx("BossExplosion64", -20, -20, 0, 0, 0, 0, 0, 130)
			64MD L 0 A_NoBlocking
			64MD L 4 A_SpawnItemEx("BossExplosion64", 20, 20, 55, 0, 0, 0, 0, 130)
			64MD L 4 A_SpawnItemEx("BossExplosion64", 0, 0, 110, 0, 0, 0, 0, 130)
			64MD MNO 6
			TNT1 A 5 A_KillMaster
			Stop
		Crush:
			A027 A 0
			A027 A 0 A_PlaySound("friendlyimp64/crush")
			A027 A -1 A_KillMaster
			Stop
		Pain.LifeLeech:
			TNT1 A 0 A_GiveToTarget("SerpentHealth", 1)
			Goto Pain		
		Pain.Vodoo:
			TNT1 A 0 A_GiveToTarget("Voodoo", 1)
			Goto Pain 
		Pain.Hexen2RavenDamage:
			TNT1 A 0 A_Jump(192,"Pain")
			TNT1 A 0 A_SpawnItemEx("Hexen2GibSpawner",0,0,0,0,0,0,0,SXF_USEBLOODCOLOR|SXF_NOCHECKPOSITION|SXF_SETMASTER)
			Goto Pain				
		Death.DukeIce:
			"####" "#" 0 A_PlaySound("duke/enemyfreeze")
			"####" "#" 0 A_GiveInventory("FreezethrowerWeakness")
			"####" "#" 0 A_Jump(218,"Ice")
			"####" "#" 0 A_GiveToTarget("DukeBossKillTaunt",1)
		Ice:
		Death.HexenIce:
		Death.WTMeleeIce:
		Death.WTRangedIce:	
		Death.Ice:
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_KillMaster
			"####" "#" 0 A_GenericFreezeDeath
			"####" "#" 140 A_UnsetSolid
			"####" "#" 0 A_PlaySoundEx("freeze/break","soundslot6",0,0)
			"####" "############################################################################" 0 A_SpawnItemEx("IceChunk2",random(-1,1),random(-1,1),random(16,96),random(-2,2),random(-2,2),random(5,15))
			"####" "#" 0 A_IceGuyDie
			"####" "#" 1 A_IceGuyDie			
			TNT1 A -1 
			Stop			
		Death.SWFist:
			TNT1 A 0 A_GiveToTarget("SWLoWangBloody", 1)
			TNT1 A 0 A_Jump(218, "Death")
			TNT1 A 0 A_SpawnItemEx("SWFisted")
			TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
			goto Death	
		Death.SWKatana:
			TNT1 A 0 A_GiveToTarget("SWLoWangBloody", 1)
			TNT1 A 0 A_Jump(218, "Death")
			TNT1 A 0 A_SpawnItemEx("SWKatanad")
			TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
			goto Death	
		Death.SWShuriken:
			TNT1 A 0 A_Jump(218, "Death")
			TNT1 A 0 A_SpawnItemEx("SWShurikend")
			TNT1 A 0 A_GiveToTarget("LoWangDMTaunt1", 1)
			goto Death
		Death.SWSticky:
		Death.SWStickyCoop:	
		Death.SWGrenade:
		Death.SWNuke:
		Death.SWRocket:
		Death.SWRailgun:
		Death.SWRailgunCoop:
		Death.SWNukeCoop:
		Death.SWFlame:
		Death.SWFlame2:
		Death.SWFlame2Coop:
		Death.SWBullet:	  	
		Death.SWFlame3:
			TNT1 A 0 A_Jump(218, "Death")
			TNT1 A 0 A_GiveToTarget("LoWangDMTaunt2", 1)
			goto Death
		Death.Vodoo:
		Death.VoodooPuff:
		Death.DynamiteCoop:
		Death.Dynamite:
		Death.LifeLeech:
		Death.CalebBullet:
		Death.CalebNapalm:
		Death.Tesla:
		Death.CalebFire:
			TNT1 A 0 A_Jump(218, "Death")
			TNT1 A 0 A_GiveToTarget("CalebDMTaunt1", 1)
			Goto Death
		Death.EradDisc:
		Death.EradShock:
		Death.EradPiercingDart:
		Death.EradExplosive:
		Death.EradNova:
		Death.EradExplosive2:	  
			TNT1 A 0 A_Jump(218, "Death")
			TNT1 A 0 A_GiveToTarget("EleenaDMTaunt2", 1)
			Goto Death
		Death.PSRAPower:
		Death.PSRAPowerCoop:
		Death.PSBullet:
		Death.PSFire:	
		Death.PSExplosive:
		Death.PSExplosiveCoop:
			TNT1 A 0 A_Jump(218, "Death")
			TNT1 A 0 A_GiveToTarget("JonDMTaunt1", 1)
			Goto Death
		Death.LeonardDamage0:
		Death.LeonardDamage1:
		Death.LeonardDamage2:
		Death.LeonardDamage3:
			TNT1 A 0 A_Jump(218, "Death")
			TNT1 A 0 A_GiveToTarget("LeonardGibTaunt", 1)
			Goto Death		
		Death.ZorchPistol:
		Death.Zorch:
		Death.ZorchKamikaze:
		Death.PhasingZorch:
		Death.ZorchSpectral:
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_Stop
			"####" "#" 0 A_ChangeFlag("DONTGIB",1)
			"####" "#" 0 A_PlaySoundEx("chex/zorchhuge","soundslot5",0,0)
			"####" "#" 3 A_SpawnItem("ZorchEffectHuge")
			"####" "#" 0 A_GiveInventory("EnemyZorched",1)
			"####" "#" 3 A_SetTranslucent(0.9,0)
			"####" "#" 0 A_Scream
			"####" "#" 3 A_SetTranslucent(0.8,0)
			"####" "#" 0 A_NoBlocking
			"####" "#" 3 A_SetTranslucent(0.7,0)
			"####" "#" 3 A_SetTranslucent(0.6,0)
			"####" "#" 3 A_SetTranslucent(0.5,0)
			"####" "#" 3 A_SetTranslucent(0.4,0)
			"####" "#" 3 A_SetTranslucent(0.3,0)
			"####" "#" 3 A_SetTranslucent(0.2,0)
			"####" "#" 3 A_SetTranslucent(0.1,0) 
			TNT1 A 1 A_SetTranslucent(1,0)
			TNT1 A -1 A_KillMaster
			stop			
		Death.DukeExplosive:			
		Death.DukeDamage:
		Death.DukePistol:
			TNT1 A 0 A_Jump(218,"Death")
			TNT1 A 0 A_GiveToTarget("DukeKillTaunt",1)
			goto Death
		Death.MightyBoot:
			TNT1 A 0 A_PlaySoundEx("duke/kickhit","body",0,0)
			TNT1 A 0 A_Jump(64,"Death")
			TNT1 A 0 A_GiveToTarget("DukeBootTaunt",1)
			goto Death			
		Death.RottDisintegrate:
			"####" "#" 0 A_ChangeFlag("SOLID", 0)
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_Scream
			"####" "#" 0 A_NoBlocking	
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 0 A_Stop
			"####" "#" 0 A_ChangeFlag("SHOOTABLE",FALSE)
			"####" "#" 0 A_ChangeFlag("NOGRAVITY",TRUE)
			"####" "#" 0 A_ChangeFlag("FLOORCLIP",TRUE)
			"####" "#" 0 A_SpawnItem("RGodModeDeathHuge")
			"####" "#" 3 A_SetTranslucent(0.9,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.8,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.7,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.6,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.5,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.4,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.3,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.2,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.1,0)
			TNT1 A 5 A_KillMaster
			stop
		Disintegrate:
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_NoBlocking
			"####" "#" 0 A_ChangeFlag("DONTGIB",1)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 0 A_Scream
			"####" "#" 0 A_ChangeFlag("SOLID",FALSE)
			"####" "#" 0 A_ChangeFlag("SHOOTABLE",FALSE)
			"####" "#" 0 A_ChangeFlag("NOGRAVITY",TRUE)
			"####" "#" 0 A_ChangeFlag("FLOORCLIP",TRUE)
			"####" "#" 0 A_Stop
			"####" "#" 3 A_SpawnItem("DisintEffectHuge",0,10)
			"####" "#" 3 A_SetTranslucent(0.9,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.8,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.7,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.6,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 3 A_SetTranslucent(0.5,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.4,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.3,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.2,0)
			"####" "#" 0 ACS_NamedExecuteAlways("ExtraDeathFX",0,1)
			"####" "#" 2 A_SetTranslucent(0.1,0)
			TNT1 A 5 A_KillMaster
			stop
		Death.Nuke:
			"####" "#" 0 A_ChangeFlag("SOLID", 0)
			"####" "#" 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			"####" "#" 0 A_Scream
			"####" "#" 0 A_NoBlocking		
			"####" "#" 0 A_SpawnItem("WTNukeDeathHuge")
			"####" "#################" 3 A_Fadeout(0.05)
			TNT1 A 5 A_KillMaster
			stop							
	}
}

ACTOR MotherBall64
{
	RenderStyle Normal
	Speed 15
	Damage 10
	Radius 11
	Height 8
	Projectile 
	+SEEKERMISSILE 
	+RANDOMIZE
	SeeSound "skeleton64/attack"
	DeathSound "doom64guy/explode"
	States
	{
		Spawn:
			64RB A 0 BRIGHT A_CustomMissile("MotherBallTrail64", 0, 0, 0, 2, 0)
			64RB A 2 BRIGHT A_SeekerMissile(5, 15)
			64RB B 0 BRIGHT A_CustomMissile("MotherBallTrail64", 0, 0, 0, 2, 0)
			64RB B 2 BRIGHT A_SeekerMissile(5, 15)
			Loop
		Death:
			64RB C 5 BRIGHT
			64RB D 3 BRIGHT A_FadeOut(0.20)
			64RB EFGH 3 BRIGHT A_FadeOut(0.20)
			Stop
	}
}

ACTOR MotherBallTrail64 : Doom64RocketSmokeTrail
{
	RenderStyle Translucent
	Alpha 0.50
	VSpeed 1
	+NOINTERACTION
	+CLIENTSIDEONLY	
	States
	{
		Spawn:
			64P3 ABCDE 2 BRIGHT
			Stop
	}
}

ACTOR MotherFire64
{
	Height 8
	Radius 6
	Damage 5
	Speed 15
	RenderStyle Translucent
	Alpha 0.50
	DamageType "Vertigo"
	SeeSound "skeleton64/attack"
	DeathSound "Null"
	PROJECTILE
	+RANDOMIZE
	+NODAMAGETHRUST
	+FLOORHUGGER
	+SEEKERMISSILE
	States
	{
		Spawn:
			64FR A 4 BRIGHT A_CustomMissile("MotherFireTrail64", 0, 0, 0, 2, 0)
			TNT1 A 0 A_SeekerMissile(5, 5)			
			64FR B 4 BRIGHT A_CustomMissile("MotherFireTrail64", 0, 0, 0, 2, 0)
			TNT1 A 0 A_SeekerMissile(5, 5)
			64FR C 4 BRIGHT A_CustomMissile("MotherFireTrail64", 0, 0, 0, 2, 0)
			TNT1 A 0 A_SeekerMissile(5, 5)			
			64FR D 4 BRIGHT A_CustomMissile("MotherFireTrail64", 0, 0, 0, 2, 0)
			TNT1 A 0 A_SeekerMissile(5, 5)			
			64FR E 4 BRIGHT A_CustomMissile("MotherFireTrail64", 0, 0, 0, 2, 0)
			TNT1 A 0 A_SeekerMissile(5, 5)			
			Loop
		Death:
			TNT1 A 5
			Stop
	}
}

ACTOR MotherFireTrail64
{
	Height 8
	Radius 6
	Speed 0
	Damage 0
	RenderStyle Translucent
	Alpha 0.60
	+NOGRAVITY
	+NOINTERACTION
	+CLIENTSIDEONLY
	States
	{
		Spawn:
			64FR ABCDE 4 BRIGHT A_FadeOut(0.20)
			Loop
	}
}

Actor MultiMotherDemon64 : MotherDemon64 { +THRUSPECIES }