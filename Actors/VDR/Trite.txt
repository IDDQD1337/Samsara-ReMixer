Actor MMRandomizer_TriteHole
{
	Height 32
	Mass 25
	Radius 16
	Species "Trite"
	Scale 0.55
	+ISMONSTER
	-COUNTKILL
	+NODAMAGE
	+THRUSPECIES
	-SHOOTABLE
	+SOLID
	const int TRITE_AMOUNT = 5;
	var int user_loopcounter;
	States
	{
		Spawn:
			VD39 L 5 A_Look
			Loop
		See:
			VD39 L 35
			TNT1 A 0 A_JumpIf(user_loopcounter >= TRITE_AMOUNT, "Remove")
			VD39 M 5 A_SetUserVar("user_loopcounter",user_loopcounter+1)
			VD39 N 5
			TNT1 A 0 A_SpawnItemEx("MMRandomizer_Trite",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_TRANSFERSPECIAL|SXF_TRANSFERPOINTERS,0,tid)
			Loop
		Remove:
			VD39 L 4 A_Fadeout(0.05)
			Loop
	}
}

Actor MMRandomizer_Trite : SamsaraMonsterDemonicTotalGib
{
	HitObituary "%o was nipped by a Trite."
	Health 30
	Radius 20
	Height 25
	Mass 100
	Speed 6
	Scale 0.55
	PainChance 192
	BloodColor DarkGreen
	PainSound "VDR/Trite/Pain"
	SeeSound "VDR/Trite/Sight"
	DeathSound "VDR/Trite/Death"
	ActiveSound "VDR/Trite/Active"
	MeleeSound "VDR/Trite/MeleeHit"
	Species "Trite"
	MeleeDamage 2
	MeleeThreshold 64
	MaxTargetRange 160
	MinMissileChance 10
	Damage 2
	Monster
	+FLOORCLIP
	+DONTHURTSPECIES
	+NOTARGET
	Tag "Trite"
	Species "SamsaraDemon"
	States
	{
		Spawn:
			VD39 A 0
			VD39 A 1 A_JumpIfInventory("AlreadySpawned",1,"Spawn2")
			VD39 A 1 ACS_NamedExecuteAlways("ReMixerSpawnScript",0,1,0)
			VD39 A 0 A_GiveInventory("AlreadySpawned",1)
		Spawn2:
			VD39 A 1 A_Look
			Loop
		See:
			VD39 A 2 A_Chase("", "")
			VD39 A 2 A_Chase
			TNT1 A 0 A_PlaySound("VDR/Trite/Step", CHAN_7)
			VD39 BB 2 A_Chase("Melee", "Missile", CHF_NOPLAYACTIVE)
			TNT1 A 0 A_PlaySound("VDR/Trite/Step", CHAN_7)
			VD39 CC 2 A_Chase
			TNT1 A 0 A_PlaySound("VDR/Trite/Step", CHAN_7)
			VD39 DD 2 A_Chase("Melee", "Missile", CHF_NOPLAYACTIVE)
			TNT1 A 0 A_PlaySound("VDR/Trite/Step", CHAN_7)
			Loop
		Melee:
			VD39 A 6 A_FaceTarget
			TNT1 A 0 A_Jump(192, 2)
			TNT1 A 0 A_PlaySound("VDR/Trite/Attack")
			VD39 E 6 A_MeleeAttack
			Goto See
		Missile:
			TNT1 A 0 A_Jump(128, "See")
			TNT1 A 0 A_ChangeFlag("NOPAIN",0)
			VD39 AAA 3 A_FaceTarget
			TNT1 A 0 A_PlaySound("VDR/Trite/Attack")
			TNT1 A 0 A_SkullAttack
			VD39 F 10 ThrustThingZ(0, 6, 0, 1)
			VD39 E 8 A_Stop
			Goto See
		Web:
			TNT1 A 0
			TNT1 A 0 A_LowGravity
			TNT1 A 0 A_ChangeFlag("NODAMAGE", 1)
			TNT1 A 0 A_ChangeFlag("NOPAIN", 1)
			TNT1 A 0 A_PlaySound("VDR/Trite/WebStart")
			VD39 F 1 A_CheckFloor(1)
			Wait
			TNT1 A 0 A_ChangeFlag("NODAMAGE", 0)
			TNT1 A 0 A_ChangeFlag("NOPAIN", 0)
			TNT1 A 0 A_Gravity
			VD39 A 12 A_PlaySound("VDR/Trite/WebEnd")
			Goto Spawn2
		Pain:
			VD39 E 3
			VD39 E 3 A_Pain
			Goto See
		Death:
			TNT1 A 0 ACS_NamedExecuteAlways("Samsara_KillCount", 0, 0)
			TNT1 A 0 A_SpawnItemEx("UniversalBloodyHellGoreDeathEffect",0,0,0,0,0,0,0,SXF_NOCHECKPOSITION|SXF_USEBLOODCOLOR)
			VD39 G 5 A_Scream
			TNT1 A 0 A_PlaySound("VDR/Trite/Splat", CHAN_7)
			VD39 H 5 A_NoBlocking
			VD39 IJK 5
			Stop
		Pain.MightyBoot:
			TNT1 A 0 A_ChangeFlag("NOPAIN",1)
			TNT1 A 0 A_PlaySound("duke/kickhit",CHAN_BODY)
			VD39 E 2 A_FaceTarget
			TNT1 A 0 A_Recoil(14)
			VD39 E 16 A_Pain
			Goto See
		Pain.DoomDamageTypeStunner:
			VD39 E 2 A_ChangeFlag("NOPAIN",true)
			VD39 E 300 A_Pain
			TNT1 A 0 A_ChangeFlag("NOPAIN",false)
			goto See
	}
}