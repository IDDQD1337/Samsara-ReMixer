Actor MMRandomizer_BruiserDemon : BaronOfHell3
{
	Health 1500
	Radius 24
	Height 64
	Mass 2000
	Speed 8
	PainChance 15
	Monster
	+BOSS
	+FLOORCLIP
	+LOOKALLAROUND
	+MISSILEEVENMORE
	+NORADIUSDMG
	MissileType MMRandomizer_BruiserBall
	SeeSound "VDR/superbaron/scream"
	PainSound "VDR/superbaron/pain"
	DeathSound "VDR/superbaron/death"
	ActiveSound "VDR/superbaron/act"
	MeleeSound "baron/melee"
	Obituary "%o was slaughtered by a Bruiser demon."
	HitObituary "%o was cremated by a Bruiser demon."
	MeleeDamage 20
	Scale 1.25
	States
	{
		Spawn:
			VD56 AB 10 Bright A_Look 
			Loop
		See:
			VD56 AABBCCDD 3 Bright A_Chase
			Loop
		Melee:
			VD56 E 6 Bright A_FaceTarget
			VD56 F 6 Bright A_FaceTarget
			VD56 G 6 Bright A_ComboAttack
			Goto See
		Missile:
			VD56 E 0 Bright A_Jump(144, 8)
			VD56 EF 6 Bright A_FaceTarget
			VD56 G 6 Bright A_MissileAttack
			VD56 G 0 Bright A_Jump(96, 1)
			Goto See
			VD56 HI 6 Bright A_FaceTarget
			VD56 J 6 Bright A_MissileAttack
			Goto See
			VD56 E 0 Bright A_Jump(96, 20)
			VD56 E 6 Bright A_FaceTarget
			VD56 F 4 Bright A_FaceTarget
			VD56 F 2 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,-40,0)
			VD56 G 1 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,-30,0)
			VD56 G 1 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,-20,0)
			VD56 G 1 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,-10,0)
			VD56 G 1 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,0,0)
			VD56 G 1 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,10,0)
			VD56 G 1 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,20,0)
			VD56 G 0 Bright A_Jump(96, 1)
			Goto See
			VD56 H 6 Bright A_FaceTarget
			VD56 I 4 Bright A_FaceTarget
			VD56 I 2 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,40,0)
			VD56 J 1 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,30,0)
			VD56 J 1 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,20,0)
			VD56 J 1 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,10,0)
			VD56 J 1 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,0,0)
			VD56 J 1 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,-10,0)
			VD56 J 1 Bright A_Custommissile("MMRandomizer_BruiserBall2",56,0,-20,0)
			Goto See
			VD56 KL 6 Bright A_FaceTarget
			VD56 M 2 Bright A_Custommissile("MMRandomizer_BruiserFireSpawner",16,0,-15,0)
			VD56 M 2 Bright A_Custommissile("MMRandomizer_BruiserFireSpawner",16,0,0,0)
			VD56 M 2 Bright A_Custommissile("MMRandomizer_BruiserFireSpawner",16,0,15,0)
			Goto See
		Pain:
			VD56 N 5 Bright A_Pain
			Goto See
		Death:
			VD57 A 6 Bright A_Scream
			VD57 BCD 4 Bright 
			VD57 EFG 4 Bright
			VD57 H 4 Bright A_Fall
			VD57 IJKLMNOP 4 Bright
			VD57 QRSTUV 4
			VD57 W 0 A_KillMaster
			VD57 W -1   
			Stop
	}
}

Actor MMRandomizer_BruiserBall
{
	Radius 16
	Height 16
	Speed 18
	Damage 16
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.9
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	Decal Scorch
	States
	{
		Spawn: 
			VE13 AABB 2 Bright A_SpawnItemEx("MMRandomizer_BruiserBallTrail", 0, 0, 0, 0, 0, 0, 0, 128)
			Loop
		Death:
			VE13 KLMNOPQRSTUVWX 3 Bright
			Stop
	}
}

Actor MMRandomizer_BruiserBall2
{
	Radius 8
	Height 8
	Speed 10
	Damage 8
	Projectile
	+RANDOMIZE
	RenderStyle Add
	Alpha 0.9
	SeeSound "imp/attack"
	DeathSound "imp/shotx"
	Decal BaronScorch
	States
	{
		Spawn: 
			VE14 AB 6 Bright 
			Loop
		Death:
			VE14 CDEFGHI 3 Bright
			Stop
	}
}

Actor MMRandomizer_BruiserFireSpawner
{
	Radius 8
	Height 8
	Speed 12
	Damage 0
	+RIPPER
	+FLOORHUGGER
	+BLOODLESSIMPACT
	Projectile
	MissileType MMRandomizer_BruiserFire
	Missileheight 0
	States
	{
		Spawn: 
			TNT1 A 3 Bright A_MissileAttack
			TNT1 A 3
			TNT1 A 3
			Loop
		Death:  
			TNT1 A 6
			Stop
	}
}

Actor MMRandomizer_BruiserBallTrail
{
	Radius 1
	Height 1
	Speed 0
	Projectile
	RenderStyle Add
	Alpha 0.70
	+NOCLIP
	States
	{
		Spawn:
			VE13 CDEFGHIJ 4 Bright
			Stop       
	}
}

Actor MMRandomizer_BruiserFire
{
	Radius 1
	Height 1
	Speed 0
	Damage 20
	Projectile
	RenderStyle Add
	Alpha 0.9
	+NOCLIP
	+FLOORHUGGER
	SeeSound "weapons/rocklx"
	States
	{
		Spawn:
			VE15 AB 3 Bright
			VE15 C 3 Bright A_Explode(48,128,0)
			VE15 DEFGHIJKLMNOPQRST 3 Bright
			Stop       
	}
}

