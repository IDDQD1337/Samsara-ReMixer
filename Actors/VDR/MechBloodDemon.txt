ACTOR MMRandomizer_MechBloodDemon : MMRandomizer_MechDemon
{ 
    Health 500
    Speed 12 
    Height 56
    Radius 30
    Mass 400
	Damage 3
    PainChance 180
    Meleedamage 5 
    ReactionTime 8 
    Monster
    +FLOORCLIP
    SeeSound "VDR/mechdemon/sight"
    PainSound "VDR/mechdemon/pain"
    DeathSound "VDR/mechdemon/death"
    ActiveSound "VDR/mechdemon/active"
    //MeleeSound "VDR/mechdemon/melee"
	Obituary "%o was gored by a Mechanical Blood Demon."
	HitObituary "%o was mauled and chewed up by a Mechanical Blood Demon"
    States 
    { 
		Spawn: 
			VD47 AB 10 A_Look
			Loop 
		See: 
			VD47 A 0 A_PlaySound("VDR/mechdemon/walk")
			VD47 AABB 2 A_Chase
			VD47 C 0 A_PlaySound("VDR/mechdemon/walk")
			VD47 CCDD 2 A_Chase
			Loop 
		Melee: 
			VD47 E 0 A_PlaySound("VDR/mechdemon/melee")
			VD47 EF 8 A_FaceTarget
			VD47 G 8 A_MeleeAttack
			Goto See 
		Missile: 
			VD47 E 0 A_JumpIfCloser(224, 2)
			Goto See
			VD47 E 4 A_FaceTarget 
			VD47 F 10 A_SkullAttack 
			VD47 G 5 A_Gravity
			Goto See 
		Pain: 
			VD47 H 2
			VD47 H 2 A_Pain
			Goto See
		Death: 
			VD47 I 8 
			VD47 I 0 A_FaceTarget
			VD47 J 0 A_SpawnItemEx("MMRandomizer_MechBloodDemonArm", 10, 0, 32, 0, 8, 0, 0, 128)
			VD47 J 8 A_Scream
			VD47 K 4 
			VD47 L 4 A_NoBlocking
			VD47 M 4 
			VD47 N -1 
			Stop 
		Raise: 
			VD47 NMLKJI 5 
			Goto See 
		} 
}

ACTOR MMRandomizer_MechBloodDemonArm
{
	Radius 8
	Height 8
	Speed 8
	+DOOMBOUNCE
	+DROPOFF
	+MISSILE
    States
	{
		Spawn:
			VD47 OPQRSTUV 2
			Loop
		Death:
			VD47 W -1
			Loop
	}
}

