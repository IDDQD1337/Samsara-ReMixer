actor MMRandomizer_Nightshade 30131
{
	Obituary "%o couldn't escape the Nightshade."
	Health 80
	Radius 20
	Height 56
	Mass 100
	Speed 12
	PainChance 160
	SeeSound "vdr/nshsit"
	PainSound "vdr/nshpai"
	DeathSound "vdr/nshdth"
	ActiveSound "vdr/nshact"
	MONSTER
	+FLOORCLIP
	States
	{
		Spawn:
			VD65 AB 10 A_Look
			loop
		See:
			VD65 A 0 A_SetTranslucent(0.75,0)
			VD65 AABBCCDD 3 A_Chase
			goto See+1
		Missile:
			VD65 E 0 A_SetTranslucent(1.0,0)
			VD65 EE 4 Bright A_FaceTarget
			VD65 F 8 Bright A_CustomMissile("MMRandomizer_ShadeMissile",32,0,0,0,0)
			VD65 B 4 A_SetTranslucent(0.87,0)
			goto See
		Pain:
			VD65 G 0 A_SetTranslucent(1.0,0)
			VD65 G 2
			VD65 G 2 A_Pain
			goto See
		Death:
			VD65 H 4 A_SetTranslucent(1.0,0)
			VD65 I 4 A_SetTranslucent(0.9,0)
			VD65 I 0 A_Scream
			VD65 J 4 A_SetTranslucent(0.8,0)
			VD65 K 0 A_NoBlocking
			VD65 K 4 A_SetTranslucent(0.7,0)
			VD65 L 4 A_SetTranslucent(0.6,0)
			VD65 M 4 A_SetTranslucent(0.5,0)
			VD65 N 4 A_SetTranslucent(0.4,0)
			VD65 O 4 A_SetTranslucent(0.3,0)
			VD65 P 4 A_SetTranslucent(0.2,0)
			stop
	}
}

ACTOR MMRandomizer_ShadeMissile : CacodemonBall
{
	Damage 4
	Speed 10
	Alpha 0.80
	DamageType normal
	+THRUGHOST
	+SEEKERMISSILE
	+FORCEXYBILLBOARD
	SeeSound "vdr/nshatk"
	DeathSound "vdr/nshexp"
	States
	{
		Spawn:
			VE21 A 5 bright A_SpawnItemEx("MMRandomizer_ShadePuff",0,0,0,0,0,0)
			VE21 A 0 bright A_Seekermissile(5,7)
			VE21 B 5 bright A_SpawnItemEx("MMRandomizer_ShadePuff",0,0,0,0,0,0)
			VE21 B 0 bright A_Seekermissile(5,7)  
			loop
		Death:
			VE21 HIJKLM 3 bright
			stop
	}
}

ACTOR MMRandomizer_ShadePuff
{   
	Radius 3
	Height 3
	RENDERSTYLE ADD
	Alpha 0.67
	+NOGRAVITY
	+NOBLOCKMAP
	+DONTSPLASH
	+FORCEXYBILLBOARD
	States
	{
		Spawn:
			NULL A 3 Bright
			VE21 CDEFG 3 BRIGHT
			Stop 
	}
}