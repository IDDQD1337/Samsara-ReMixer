Actor MMRandomizer_SoulHarvester
{
    Health 100
    Radius 20
    Height 56
    Speed 8
    PainChance 160
    Monster
    +FLOORCLIP
    SeeSound "VDR/harvester/sight"
    PainSound "imp/pain"
    DeathSound "VDR/harvester/death"
    ActiveSound "imp/active"
    MeleeSound "imp/melee"
    Obituary "%o couldn't evade the soul harvester's attack."
    MissileType MMRandomizer_SoulHarvesterBall
    MeleeDamage 3
    States
    {
		Spawn:
			VD40 AB 10 A_Look
			Loop
		See:
			VD40 AABBCCDD 3 A_Chase
			Loop
		Melee:
		Missile:
			VD40 EFG 4 A_FaceTarget
			VD40 HIJK 5 Bright A_FaceTarget
			VD40 L 8 Bright A_MissileAttack
			VD40 M 10
			Goto See
		Pain:
			VD40 N 3
			VD40 N 3 A_Pain
			Goto See
		Death:
			VD40 O 7 A_Scream
			VD40 PQR 7 
			VD40 S 6 A_SpawnItemEx("MMRandomizer_SoulHarvesterGhost", 0, 0, 32, 0, 0, 0, 0, 128)
			VD40 T 5 A_Fall
			VD40 UV 5
			VD40 W -1
			Stop
		XDeath:
			VD40 X 6 A_Xscream
			VD40 Y 6 A_SpawnItemEx("MMRandomizer_SoulHarvesterGhost", 0, 0, 32, 0, 0, 0, 0, 128)
			VD40 Z 6
			VD40 [ 6 A_Fall
			VD40 ] 6
			VD39 Z -1
			Stop
		Raise:
			VD40 WVUTSRQPO 5
			Goto See
    }
}

Actor MMRandomizer_SoulHarvesterBall
{
    Radius 8
    Height 6
    Scale 0.4
    Speed 9
    Damage 2
    +SEEKERMISSILE
    Projectile
    RenderStyle Add
    SeeSound "VDR/harvester/scream"
    DeathSound "imp/shotx"
	Decal DoomImpScorch
    States
    {
		Spawn:
			VE05 A 0 Bright A_SpawnItemEx("MMRandomizer_SoulBallTrail", 0, 0, 0, 0, 0, 0, 180, 128)
			VE05 AB 2 Bright A_SeekerMissile (10,20)
			Loop
		Death:
			VE05 CDEFGHIJKL 4 Bright
			Stop
    }
}

Actor MMRandomizer_SoulHarvesterGhost
{
    Radius 1
    Height 1
    Speed 0
    Projectile
    RenderStyle Add
    Alpha 0.7
    +NOCLIP
    SeeSound "VDR/harvester/ghost"
    States
    {
		Spawn:
		Death:
			VE05 M 0
			VE05 M 0 A_PlaySound("VDR/Harvester/Ghost")
			VE05 MNOPQRS 4 Bright
			Stop
    }
}

Actor MMRandomizer_SoulBallTrail
{
    Radius 1
    Height 1
    Speed 0
    Projectile
    RenderStyle Add
    Alpha 0.5
    +NOCLIP
    States
    {
		Spawn:
		Death:
			VE06 H 6 
			VE06 ABCDEFG 4 Bright
			Stop
    }
}