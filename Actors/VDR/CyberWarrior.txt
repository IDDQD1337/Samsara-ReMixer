ACTOR MMRandomizer_CyberWarrior
{
	Health 600
	Radius 24
	Height 64
	Mass 1200 
	Speed 8
	PainChance 80
	Monster
	+FLOORCLIP
	+DONTHARMCLASS
	+DONTHARMSPECIES
	+DONTHURTSPECIES
	+DONTBLAST
	+USEKILLSCRIPTS
	Obituary "%o played dodgeball with a hell paladin and lost."
	HitObituary "A Hell Paladin blew %o away."
	SeeSound "VDR/Paladin/Sight" 
	PainSound "VDR/Paladin/Pain" 
	DeathSound "VDR/Paladin/Death" 
	ActiveSound "VDR/Paladin/Active" 
	Tag "Cyber Warrior"
	States 
	{ 
		Spawn: 
			VD52 AB 10 A_Look 
			Loop 
		See:
			TNT1 A 0 A_UnSetInvulnerable
			VD52 AABB 3 Fast A_Chase
			TNT1 A 0 A_PlaySound("VDR/Paladin/Step")
			VD52 CCDD 3 Fast A_Chase
			TNT1 A 0 A_PlaySound("VDR/Paladin/Step")
			TNT1 A 0 A_Jump(32, "SeeShield")
			Goto See+1
		SeeShield:
			TNT1 A 0 A_SetInvulnerable
			VD52 EEFF 4 Fast A_Chase
			TNT1 A 0 A_PlaySound("VDR/Paladin/Step")
			VD52 GGHH 4 Fast A_Chase
			TNT1 A 0 A_PlaySound("VDR/Paladin/Step")
			TNT1 A 0 A_Jump(96, "See")
			Goto SeeShield
		Melee:
			TNT1 A 0 A_UnSetInvulnerable
			VD52 MN 6 Fast A_FaceTarget
			TNT1 A 0 A_CustomMissile("MMRandomizer_PaladinBash")
			VD52 O 6 Fast A_CustomMeleeAttack(random(1, 10) * 4 )
			TNT1 A 0 A_Jump(128, "Missile")
			Goto See
		Missile: 
			TNT1 A 0 A_UnSetInvulnerable
			TNT1 A 0 A_Jump(64, "Missile2")
			TNT1 A 0 A_JumpIfCloser(720, "Missile1")
			Goto Missile2
		Missile1: 
			TNT1 A 0 A_Jump(32, "Missile2") 
			VD52 I 5 Fast A_FaceTarget
			TNT1 A 0 A_JumpIfCloser(360, "MissileLow")	
			VD52 I 5 Fast A_FaceTarget	
			TNT1 A 0 A_PlaySound("VDR/Paladin/Grenade")			
			VD52 J 4 Fast Bright A_CustomMissile("MMRandomizer_PaladinGrenadeThrust", 30, 16, 0)
			VD52 J 4 Fast Bright A_FaceTarget
			VD52 II 5 Fast A_FaceTarget
			Goto See
		MissileLow: 
			VD52 I 5 Fast A_FaceTarget
			TNT1 A 0 A_PlaySound("VDR/Paladin/Grenade")
			VD52 J 4 Fast Bright A_CustomMissile("MMRandomizer_PaladinGrenade", 30, 16, 0)
			VD52 J 4 Fast Bright A_FaceTarget
			VD52 II 5 Fast A_FaceTarget
			Goto See
		Missile2: 
			TNT1 A 0 A_SetInvulnerable
			VD52 KK 5 Fast A_FaceTarget      
			VD52 L 4 Fast Bright A_CustomMissile("MMRandomizer_PaladinBall",32,0,0,0)
			VD52 KK 4 Fast A_FaceTarget      
			VD52 L 4 Fast Bright A_CustomMissile("MMRandomizer_PaladinBall",32,0,0,0)
			VD52 KK 4 Fast A_FaceTarget      
			VD52 L 4 Fast Bright A_CustomMissile("MMRandomizer_PaladinBall",32,0,0,0)
			VD52 KK 4 Fast A_FaceTarget      
			VD52 L 4 Fast Bright A_CustomMissile("MMRandomizer_PaladinBall",32,0,0,0)
			VD52 KK 4 Fast A_FaceTarget      
			VD52 L 4 Fast Bright A_CustomMissile("MMRandomizer_PaladinBall",32,0,0,0)
			VD52 KK 4 Fast A_FaceTarget  
			TNT1 A 0 A_UnSetInvulnerable
			Goto See
		Pain:
			VD52 P 3 Fast A_Pain
			TNT1 A 0 A_JumpIfCloser(512, "ClosePain")
			VD52 P 3 Fast
			TNT1 A 0 A_Jump(96, "See")
			TNT1 A 0 A_SetInvulnerable
			VD52 KKKKKKKKKKKKKKKKKKKK 3 A_FaceTarget
			TNT1 A 0 A_Jump(160, "SeeShield")
			TNT1 A 0 A_UnSetInvulnerable
			Goto See
		ClosePain: 
			VD52 P 3
			TNT1 A 0 A_Jump(184, "See")
			TNT1 A 0 A_SetInvulnerable
			VD52 KKKKKKKKKKKKKKKKKKKK 3 A_FaceTarget
			TNT1 A 0 A_Jump(48, "SeeShield")
			TNT1 A 0 A_UnSetInvulnerable
			Goto See
		Death:
			VD52 Q 7 A_Scream
			VD52 R 7 A_SpawnItemEx("MMRandomizer_PaladinShield", 0, 0, 25, 6, 0, 0, 60, SXF_SETMASTER|SXF_CLIENTSIDE)
			VD52 S 7
			VD52 T 7 A_NoBlocking
			VD52 UV 7
			VD52 W -1 A_BossDeath
			Stop
		Raise: 
			VD52 VUTSRQ 7
			Goto See 
	}
}

ACTOR MMRandomizer_PaladinGrenade 
{
	Radius 8
	Height 8
	Speed 25
	Damage (5 * random(4, 6))
	Projectile
	-NOGRAVITY
	+GRENADETRAIL
	+THRUGHOST
	+FORCEXYBILLBOARD
	+DONTBLAST
	+DEHEXPLOSION
	BounceType "Doom"
	Gravity 0.67
	DeathSound "VDR/Paladin/Explode"
	BounceSound "VDR/Paladin/Bounce"
	States 
	{
		Spawn:
			VD49 "[" 1 Bright A_SpawnItem("MMRandomizer_PaladinGrenadeTrail")
			Loop
		Death:
			TNT1 A 0 A_ChangeFlag ("NoGravity", 1)
			TNT1 A 0 A_Stop
			TNT1 A 0 A_SetTranslucent(1,1)
			MISL B 8 Bright A_Explode
			MISL C 6 Bright
			MISL D 4 Bright
			Stop
	}
}

Actor MMRandomizer_PaladinGrenadeThrust : MMRandomizer_PaladinGrenade 
{
	States 
	{
		Spawn:
			TNT1 AA 0 ThrustThingZ(0, 16, 0, 0)
			Goto Super::Spawn
	}
}

Actor MMRandomizer_PaladinGrenadeTrail 
{
	Scale 0.5
	Height 1
	Radius 1
	Renderstyle Add
	Alpha 0.67
	Speed 0
	+NOGRAVITY
	+NOTELEPORT
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			TNT1 A 2
			VD49 VWXYZ 2 Bright
			Stop
	}
}

ACTOR MMRandomizer_PaladinBall 
{
	Radius 6
	Height 8
	Speed 18
	Damage (8 * random(2, 4))
	Projectile
	RenderStyle Add
	Alpha 0.67
	+SeekerMissile
	+FORCEXYBILLBOARD
	+DONTBLAST
	SeeSound "VDR/Paladin/Shoot"
	DeathSound "VDR/Paladin/Hit"
	States 
	{
		Spawn:
			VE11 A 2 Bright A_SeekerMissile(2, 2)
			VE11 A 0 Bright A_SpawnItemEx("MMRandomizer_PaladinBallTrail",0,0,0,0,0,0,0,128)
			VE11 B 2 Bright A_SeekerMissile(2, 2)
			VE11 B 0 Bright A_SpawnItemEx("MMRandomizer_PaladinBallTrail",0,0,0,0,0,0,0,128)
			VE11 C 2 Bright A_SeekerMissile(2, 2)
			VE11 C 0 Bright A_SpawnItemEx("MMRandomizer_PaladinBallTrail",0,0,0,0,0,0,0,128)
			VE11 D 2 Bright A_SeekerMissile(2, 2)
			VE11 D 0 Bright A_SpawnItemEx("MMRandomizer_PaladinBallTrail",0,0,0,0,0,0,0,128)
			Loop
		Death:
			VE11 EFHIJ 3 Bright
			Stop
	}
}

actor MMRandomizer_PaladinBash 
{
	Radius 5
	Height 5
	Speed 28
	Damage (5 * random(1, 2))
	DeathSound "VDRPaladin/Melee"
	PROJECTILE
	+SEEKERMISSILE
	+DONTBLAST
	States 
	{
		Spawn:
			TNT1 AA 1 A_SeekerMissile(90, 90, SMF_PRECISE)
			stop
		Death:
			TNT1 A 0 A_Quake(4, 17, 0, 256)
			TNT1 A 1 A_Blast(0, 4, 40, 20)
			TNT1 A 1
			Stop
	}
}

Actor MMRandomizer_PaladinBallTrail 
{   
	Radius 3
	Height 3
	Scale 1
	Speed 0
	+NOGRAVITY
	+FORCEXYBILLBOARD
	+CLIENTSIDEONLY
	RenderStyle Add
	Alpha 0.67
	States 
	{
		Spawn:
			TNT1 A 2 Bright
			VE11 KLMNOP 2 Bright
			Stop 
	}
}

ACTOR MMRandomizer_PaladinShield 
{
	Radius 8
	Height 8
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			VD49 MNOPQRST 3 A_CheckFloor("Ground")
			Loop
		Ground:
			VD49 U 2100 A_SetFloorClip
			VD49 U 5 A_FadeOut(0.1)
			Wait
		Death:
			TNT1 A 0 A_SpawnItemEx("MMRandomizer_PaladinShieldFade")
			Stop
	}
}

Actor MMRandomizer_PaladinShieldFade 
{
	Radius 8
	Height 8
	+CLIENTSIDEONLY
	States 
	{
		Spawn:
			VD49 U 1
			VD49 U 1 A_FadeOut(0.01) 
			Wait
	}
}