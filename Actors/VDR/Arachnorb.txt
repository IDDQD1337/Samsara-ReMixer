actor MMRandomizer_Arachnorb
{
	MONSTER
	+NOGRAVITY
	+FLOAT
	+FLOATBOB
	Health 170
	Radius 24
	Height 56
	Mass 400
	Speed 12
	PainChance 150
	SeeSound "VDR/aracnorb/sight"
	ActiveSound "baby/active"
	PainSound "baby/pain"
	DeathSound "VDR/aracnorb/death"
	MeleeSound "VDR/aracnorb/melee"
	MeleeDamage 6
	MissileHeight 36
	MissileType MMRandomizer_AracnorbBall
	Obituary "%o suffered psychic trauma from an aracnorb's brainwaves."
	HitObituary "%o had %p skull chewed by an aracnorb."
	Scale 0.85
	States
	{
		Spawn:
			VD66 A 1 A_Look
			Loop
		See:
			VD66 A 2 A_Chase
			Loop
		Melee:
			VD66 AB 5
			VD66 C 6 A_MeleeAttack
			Goto See
		Missile:
			VD66 B 12 Bright A_FaceTarget
			VD66 C 2 Bright A_MissileAttack
			VD66 B 2 Bright
			VD66 D 0 A_Jump(32,"See")
			VD66 D 0 A_SpidRefire
			Goto Missile+1
		Pain:
			VD66 I 2
			VD66 I 2 A_Pain
			Goto See
		Death:
			VD66 D 0 A_ChangeFlag("FLOATBOB",0)
			VD66 D 0 A_Scream
			VD66 D 6 A_Fall
			VD66 D 1
			Wait
		Crash:
			VD66 EFG 6
			VD66 H -1
			Stop
			Raise:
			VD66 HGFEDA 8
			VD66 A 0 A_ChangeFlag("FLOATBOB",1)
			Goto See
	}
}

actor MMRandomizer_AracnorbBall
{
	Radius 13
	Height 8
	Speed 8
	Damage 3
	RenderStyle Add
	Alpha 0.75
	SeeSound "baby/attack"
	DeathSound "baby/shotx"
	PROJECTILE
	+STRIFEDAMAGE
	+SEEKERMISSILE
	+RANDOMIZE
	States
	{
		Spawn:
			VE22 AABB 1 Bright A_BishopMissileWeave
			Loop
		Death:
			VE22 CDEFG 5 Bright
			Stop
	}
}
